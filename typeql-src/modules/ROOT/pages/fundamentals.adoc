= Fundamentals
:keywords: typedb, typeql, documentation, overview, introduction
:longTailKeywords: documentation overview, learn typedb, learn typeql, typedb schema, typedb data model
:pageTitle: Fundamentals
:summary: TypeQL essentials

All queries to a TypeDB database are written in TypeQL.

TypeQL is a *declarative* query language.
Rather than writing an algorithm of how the data should be retrieved, we declare requirements, and the TypeDB query
processor will take care of finding an optimal way to retrieve and process the data.

[NOTE]
====
TypeQL is both a https://en.wikipedia.org/wiki/Data_definition_language[Data Definition Language,window=_blank] (DDL)
and https://en.wikipedia.org/wiki/Data_manipulation_language[Data Manipulation Language,window=_blank] (DML).
====

As a DDL, it lets us define a <<_schema,schema>> of a database.

As a DML, it lets us query (read&write) <<_data,data>> of a database.

The complete set of the TypeQL grammar can be seen in the *ANTLR v.4* specification file:
https://github.com/vaticle/typeql/blob/master/grammar/TypeQL.g4[TypeQL.g4,window=_blank].

== Query structure

A TypeQL xref:typeql::queries.adoc[query] consists of clauses (one, two or three).
There are the following types of clauses:

* `define`
* `undefine`
* `match`
* `fetch`
* `get`
* `insert`
* `delete`

All clauses start with the keyword, the same as the clause's name, followed by:

* A list of variables and optional modifiers at the end (for a `get` clause);
* A list of variables or subqueries (for a `fetch` clause);
* A TypeQL pattern (for all other clauses).

A pattern consists of one or more statements.

.TypeQL query structure
* Query
** Clause
*** Pattern
**** Statement

.See a simple query example
[%collapsible]
====
[,typeql]
----
match $p isa person;
get $p;
limit 1;
----

In the example above we can see a single xref:typeql::data/get.adoc[Get] query with two clauses: `match` and `get`.
The query also has a modifier to xref:typeql::data/get.adoc#_limit_the_results[limit] the number of results returned
to exactly one.
====

A string or a file with multiple TypeQL queries is a script.

.See a TypeQL script example
[%collapsible]
====
[,typeql]
----
insert
$p isa person,
    has full-name "Masako Holley",
    has email "masako.holley@vaticle.com";

insert
$p isa person,
    has full-name "Pearle Goodman",
    has email "pearle.goodman@vaticle.com";

insert
$p isa person,
    has full-name "Kevin Morrison",
    has email "kevin.morrison@vaticle.com";
----

The above example contains three xref:typeql::data/insert.adoc[insert] queries without a match clause.

To rewrite it as a single query we need to remove the second and third `insert` keyword and change variables, so that
they are not overlapping (otherwise, we will insert a single person entity with three full-names and three emails):

[,typeql]
----
insert
$p1 isa person,
    has full-name "Masako Holley",
    has email "masako.holley@vaticle.com";

$p2 isa person,
    has full-name "Pearle Goodman",
    has email "pearle.goodman@vaticle.com";

$p3 isa person,
    has full-name "Kevin Morrison",
    has email "kevin.morrison@vaticle.com";
----
====

For more information on patterns, see the xref:typeql::data/basic-patterns.adoc[]
and the xref:typeql::data/advanced.adoc[] pages.

[#_schema]
== Schema

TypeDB schema is like a blueprint of a database.

A database schema contains all user-defined <<_types,types>> and <<_rules,rules>> used in a database.

Every instance of data inserted in a TypeDB database must be assigned a type and be validated against constraints.

[NOTE]
====
Database schemas written in TypeQL resemble logical data models of relational databases.
TypeDB removes the need to create a separate physical data model and allows developers to create schemas that mirror
their object model, simplifying design and development.

As a result, TypeDB schemas look like entity-relationship (ER) diagrams with entities, relations, and attributes
connected by roles of relations and ownerships of attributes, but also subtyping (see <<_inheritance,inheritance>>
below).
====

[#_types]
=== Types
// tag::types_basics[]
A type represents a set of constraints on the interpretation of data.

A type for a data instance is like a class for an object in OOP.

A definition of a type in a schema of a database sets:

* type hierarchy -- every user-defined type has only one parent or supertype.
* constraints -- what a type can do: what role to play, what attributes it can own, what value type it has
(attribute types only), and what roles it relates (relation types only).

A type can be addressed by its label (name). A type label is unique in a schema of a database.

We can define a new type only as a subtype of an existing one.

A new empty database has a set of built-in types. These built-in types are called root types because all user-defined
types will be subtypes (direct or non-direct) of those root types.

.Root (built-in) types
[cols="^.^1, ^.^3, ^.^4",options="header"]
|===
| Root type label | A subtype of the root type | An instance of data of a subtype

| `entity`
| Entity type
| Instance of entity type

| `relation`
| Relation type
| Instance of relation type

| `attribute`
| Attribute type
| Instance of attribute type
|===

[IMPORTANT]
====
All root types are <<_abstract_types,abstract>> types.

That's why when we say *entity type*, we usually mean a subtype of the `entity` root type.
In the same way, we can address *relation type* or *attribute type* instead of mentioning
subtypes of the root types.
====
// end::types_basics[]

[NOTE]
====
To avoid ambiguity when using type labels, we should try to provide context or specify what exactly
we mean:

- the type and all its subtypes (the default variant),
- the exact type, without any of its subtypes,
- or instances of data of this particular type and instances of data of all its subtypes.
====

[#_inheritance]
=== Inheritance
// tag::inheritance_basics[]

A type can subtype another type. As a result, the subtype inherits all the attributes owned and roles played by its
supertype.

[NOTE]
====
Roles can be inherited and even overridden as a part of relation inheritance.
====

Type can only have a single supertype.

Types can be subtypes of other subtypes, resulting in a type hierarchy.

.See simple example
[%collapsible]
====
For example, `business-unit` subtypes `user-group`, which subtypes `subject`, which subtypes `entity` root type.
Also, `person` subtypes `user`, that subtypes `subject`:

* entity
** subject
*** user group
**** business unit
*** user
**** person

In TypeQL we can define these types with the following query:

[,typeql]
----
define

subject sub entity;
user-group sub subject;
business-unit sub user-group;
user sub subject;
person sub user;
----
====

There is a strict hierarchy of types, so the whole typing system of a TypeDB database can always be represented
by three independent trees with one of the root types at the top of each tree.

.See hierarchy trees example
[%collapsible]
====
For example, a schema with the following types:

* entity
** person
** vehicle
*** car
*** motorcycle
*** bicycle

* relation
** owning
** using
*** driving
*** traveling

* attribute
** model
** name
*** full-name
*** nickname

can be visualized as following type hierarchy:

image::root-types-trees.png[]
====
// end::inheritance_basics[]

[#_abstract_types]
=== Abstract types
// tag::abstract_basics[]

An abstract type can't be instantiated (we can't insert an instance of data of this type). All we can do with an
abstract type is to subtype it.

All root types are abstract types.

The opposite of an abstract type is a concrete type.

All user-defined types are concrete types by default.
// end::abstract_basics[]

[#_thing_type]
=== Thing type
// tag::thing_type_basics[]

An internal type called `thing` can be used to address all types (both built-in and user-defined) or instances of all
types (effectively — all data).

All types are subtypes of the `thing`, including the root types.

image::thing-era-role-rule.png[Types hierarchy]

// #todo Redraw the diagram. Without the Thing. Consider using PlantUML

include::typedb::partial$thing-warning.adoc[]
// end::thing_type_basics[]

[#_entity_types]
=== Entity types
// tag::entity_type_basics[]

*Entity types* (or subtypes of the `entity` root type) represent the classification of independent objects in the
data model of our business domain.

Instance of data of an entity type represents a standalone object that exists in our data model independently.

Instance of an entity type doesn't have a value.
It is usually addressed by its ownership over attribute instances and/or roles played in relation instances.

An object modeled with an entity type might practically require other entities to exist, such as a car that cannot
exist without its parts, but can be conceptualized without reference to those other entities: a car can be imagined
without considering its parts.

// end::entity_type_basics[]

.See example
[%collapsible]
====
Given the schema:

[,typeql]
----
define

name sub attribute, value string;

person sub entity,
    owns name,
    plays marriage:spouse;

marriage sub relation,
    relates spouse;
----

An instance of the `person` type can be inserted in a database:

* without owning any instances of the `name` attribute type, nor playing any roles,
* owning exactly one instance of the `name` attribute type with some value and playing a role of a spouse in a marriage
  relation,
* owning multiple names and/or playing the role `spouse` in multiple instances of the `marriage` relation type.
====
//#todo Improve the example

To define a new entity type, we need to set its label and what type it's a subtype of.

To set a property of an entity (like a name of a person), we need to define ownership by this entity of an instance
of an `attribute` type with the required value.

To define a relationship between an entity and some other user-defined types, we need to define a relation with roles
and the ability of the involved types to play those roles.

.Entity types and instances example
[%collapsible]
====
For example, there could be entity types like `company` and `person`.

Given the `company` entity type defined in a database schema, we can insert instances of data of this type in such
a database. Every instance of the `company` type inserted into the database will represent a company, that can be
addressed by whatever attributes it has (e.g., name, registration number), or by roles played in relations (e.g.,
`employer` for the particular instance of `person` entity type in an `employment` relation type).

image::instances-example.png[]

On the above image two instances of `company` type are called `Company #1` and `Company #2`, while in real life
scenario in a TypeDB database there is almost no way to differentiate between those two instances if they have no
attributes and do not participate in any relations. The only information we can get from existence of two instances
is that there are two distinct objects. But its really hard to tell which one is which without any additional
related data inserted.
====

For more information on how to define an `entity` type, see the
xref:typeql::schema/define-types.adoc#_define_entity_types[Define entity types] section on the
xref:typeql::schema/define-types.adoc[] page.

=== Relation types
// tag::relation_type_basics[]

Relation types (or subtypes of the `relation` root type) represent relationships between types.
Relation types have roles.

Other types can play roles in relations if it’s mentioned in their definition. An instance of another type
can be a role player for a role in the instance of a relation.

An instance of a relation can be uniquely addressed by a combination of its type, owned attributes, and role players.

A `relation` type must specify at least one role. A relation cannot be conceptualized without at least some of its
role players.

// end::relation_type_basics[]

// tag::relation_type_examples[]

.See the group-membership example
[%collapsible]
====
For example, given the schema:

[,typeql]
----
define

group-membership sub relation,
    relates user-group,
    relates group-member;

user-group sub entity
    plays group-membership:user-group;

subject sub entity,
    plays group-membership:group-member;
user sub subject;
----

`group-membership` is a `relation` type that defines `user-group` and `group-member` roles.
The `user-group` role is to be played by a `user-group` entity type whereas the `group-member` role is to be played by
a `subject` type and all its subtypes entities.
====
// end::relation_type_examples[]

==== Roles

Roles are special internal types used by relations. We can't create an instance of a role in a database.
But we can set an instance of another type (role player) to play a role in a particular instance of a relation type.

Roles allow a schema to enforce logical constraints on types of role players.

.See the example of a role player's type constraint
[%collapsible]
====
For example, given the schema below, a `file` type entity can't play any role in a `group-membership` relation,
and a `user` type entity can play the `group-member` role of a `group-membership` relation because it inherits it
from the `subject` type.

[,typeql]
----
define

group-membership sub relation,
    relates user-group,
    relates group-member;

user-group sub entity
    plays group-membership:user-group;

subject sub entity,
    plays group-membership:group-member;
user sub subject;

file sub entity;
----
====

==== Abstract roles

[IMPORTANT]
====
Since TypeDB v.`2.22.0`, all roles are concrete, even in <<_abstract_types,abstract>> relations.
====

.See the example of a concrete role in abstract type
[%collapsible]
====
Given the following schema:

[,typeql]
----
define

membership sub relation,
    abstract,
    relates member;

group-membership sub membership;
----

The concrete relation type `group-membership` inherits the `member` role from the abstract relation
type `membership`.

Before version `2.22.0` of TypeDB, roles of abstract relations are also abstract.
It would make the role `member` abstract.
//Even if it is inherited by a concrete relation, an abstract role continues to be abstract.
//We can't use an abstract role to insert an instance of a relation type.
We have to xref:typeql::schema/define-types.adoc#_overrides_a_role[override]
an abstract role with a new concrete role to use it for data.

////
[,typeql]
----
define

group-membership sub membership,
    relates group-member as member;
----

Then we can insert an instance of the `group-membership` type with the new role:

[,typeql]
----
insert
(group-member: $x) isa group-membership;
----
////

Since version `2.22.0` of TypeDB, all roles are concrete.
We can use a role inherited from an abstract relation directly without the need to override it with a new role:

[,typeql]
----
insert
(member: $x) isa group-membership;
----
====

[WARNING]
====
Databases created in versions of TypeDB prior to `2.22.0` will continue to use abstract roles for abstract relation,
defined in schema prior to update for version `2.22.0` or newer.

The best way to switch to the new behavior is to install the TypeDB v.`2.22.0+`, create a new database,
define the same schema, and load the same data into it.

.See the alternative way to manually upgrade database schema
[%collapsible]
=====
The alternative way to migrate to the new behaviour is to reset (unset and set) the abstractness of each abstract
relation type via the Driver API methods:

.Abstract methods
[cols="^.^1,^.^2,^.^2,^.^2",caption="",options="header"]
|===
| Method | TypeDB Java Driver | TypeDB Python Driver | TypeDB Node.js Driver

| Is `abstract`
| xref:clients::java-driver/api-reference.adoc#_check_if_type_is_abstract_local[isAbstract();]
| xref:clients::python-driver/api-reference.adoc#_check_if_type_is_abstract_local[is_abstract()]
| xref:clients::nodejs-driver/api-reference.adoc#_check_if_type_is_abstract_local[abstract;]

| Set `abstract`
| xref:clients::java-driver/api-reference.adoc#_set_abstract[setAbstract();]
| xref:clients::python-driver/api-reference.adoc#_set_abstract[set_abstract()]
| xref:clients::nodejs-driver/api-reference.adoc#_set_abstract[setAbstract();]

| Unset `abstract`
| xref:clients::java-driver/api-reference.adoc#_unset_abstract[unsetAbstract();]
| xref:clients::python-driver/api-reference.adoc#_unset_abstract[unset_abstract()]
| xref:clients::nodejs-driver/api-reference.adoc#_unset_abstract[unsetAbstract();]
|===

.Pseudocode example
[,python]
----
for rel_type in relation_types:
  if rel_type.is_abstract():
    rel_type.unset_abstract()
    rel_type.set_abstract()
commit()
----
=====
====

////
.See example
[%collapsible]
====
Given the schema:

[,typeql]
----
define

name sub attribute, value string;

person sub entity,
  owns name,
  playes spouce:marriage;

marriage sub relation,
  relates spouse;
----

An instance of a `marriage` relation type can be addressed by the instances of the `person` entity type that play the
role of spouses.

It is impossible to imagine a marriage without considering its spouses.
====
//#todo Improve the example
////

For more
information on how to define a `relation` subtype, see the
xref:typeql::schema/define-types.adoc#_define_relation_types[Define relation types] section on the
xref:typeql::schema/define-types.adoc[] page.

[#_attribute_types]
=== Attribute types
// tag::attribute_type_basics[]

Attribute types (or subtypes of the `attribute` root type) represent properties that other types can own.

Attribute types have a value type, and instances of attribute types have a value.
This value is fixed and unique for every given instance of the attribute type.

Other types can own an attribute type.
That means that instances of these other types can own an instance of this attribute type.
This usually means that an object in our domain has a property with the matching value.

An instance of an attribute type can be uniquely addressed by its type and value.

There can't be a second instance of the same type with the same value.

Multiple types can own the same attribute type -- and different instances of the same type or different types can
share ownership of the same attribute instance.

// end::attribute_type_basics[]

For more information on the types of values, attributes can have: see the
xref:typeql::schema/define-types.adoc#_value_types[list of value types] on the
xref:typeql::schema/define-types.adoc[] page.

// tag::attribute_type_example[]

.See example
[%collapsible]
====
Given the schema:

[,typeql]
----
define

name sub attribute, value string;

person sub entity,
    owns name;
----

An instance of an attribute type `name` with a value "Bob" can be owned by:

* no one (no instance of the `person` type owns the instance of the `name` type with value "Bob"),
* one particular person (there is one person with such a name),
* or multiple people (there are multiple people with the name `Bob`. All of the `pesron` type instances have ownership of
  the same instance of the `name` type with the value "Bob").
====
// end::attribute_type_example[]

// tag::attribute_type_own_depr[]
[WARNING]
====
The feature of an attribute type owning another attribute type will be deprecated in one of the upcoming versions and
deleted in TypeDB version 3.0.
====
// end::attribute_type_own_depr[]

For more information on how to define an `attribute` subtype, see the
xref:typeql::schema/define-types.adoc#_define_attribute_types[Define attribute types] section on the
xref:typeql::schema/define-types.adoc[] page.

[#_rules]
=== Rules
// tag::rules_basics[]

Rules are a part of schema and define embedded logic.

The reasoning engine uses rules as a set of logic to infer new data.

Rules can dramatically shorten complex queries, perform xref:typedb::development/infer.adoc#_explain_query[explainable]
knowledge discovery, and implement business logic at the database level.

A rule consists of a *condition* and a *conclusion*.

A *condition* is a pattern to look for in data.

A *conclusion* is a pattern to insert virtual (inferred) data for every result matched with a pattern from the
*condition*.

[IMPORTANT]
====
Inference can only be used in a read transaction.

Rules can't change persisted data in a database.

All reasoning is done within a dataset of a transaction.
====

The rules syntax uses `when` and `then` keywords for condition and conclusion, respectively.

.Rule syntax
[,typeql]
----
rule rule-label:
when {
    ## The condition
} then {
    ## The conclusion
};
----

The conclusion can be used to create a single virtual instance of data: a relation or ownership of an attribute.

Queries use rules for xref:typedb:ROOT:development/infer.adoc[Inferring new data] only in read transactions and only if
the inference option is *enabled*.

// end::rules_basics[]

.See an example of a rule
[%collapsible]
====
[,typeql]
----
rule add-view-permission: when {
    $modify isa action, has name "modify_file";
    $view isa action, has name "view_file";
    $ac_modify (object: $obj, action: $modify) isa access;
    $ac_view (object: $obj, action: $view) isa access;
    (subject: $subj, access: $ac_modify) isa permission;
} then {
    (subject: $subj, access: $ac_view) isa permission;
};
----

See the xref:typedb::tutorials/iam-schema.adoc#_add_view_permission_rule_explanation[explanation].
====

We can use xref:data/basic-patterns.adoc#_computation[computation] operations and functions in the condition pattern.
And we can use value variables in the conclusion of a rule.

// tag::recursive_logic[]
[WARNING]
====
It is possible to create a recursive logic in the line of `n = n +1` by assigning attribute ownership with the value
of a value variable. If triggered, such a rule can run indefinitely while the transaction lasts and can cause an
out-of-memory error.
====
// end::recursive_logic[]

For more information on how to create rules in a schema, see the xref:typeql::schema/define-rules.adoc[] page.

[#_data]
== Data

Every piece of data stored in a TypeDB database must be an instance of a <<_types,type>>
xref:typeql::schema/define-types.adoc[defined] in the schema of the database.

In other words, to insert data into a TypeDB database, we must use the types defined in the schema of the database.

The schema defines the vocabulary we use to query our data. See the <<_data_example,example>> below.

For more information about reading and writing data, see the following pages:

* Writing data:
** xref:typeql::data/insert.adoc[]
** xref:typeql::data/delete.adoc[]
** xref:typeql::data/update.adoc[]
* Reading data:
** xref:typeql::data/get.adoc[]

[#_data_example]
=== Example

Given the following schema:
[,typeql]
----
define

person sub entity,
    owns name,
    owns age,
    owns certified-fortune-teller;

name sub attribute, value string;
age sub attribute, value long;
certified-fortune-teller sub attribute, value boolean;
----

The simplest xref:typeql::data/insert.adoc[Insert query] can look like this:

[,typeql]
----
insert $p isa person;
----

By inserting information, that some unbound variable `isa` *<type>* we create an instance of the type. The `person` is
the type label in this example.

But what is the use of an instance of an entity type without any attributes owned or roles played in relations?

A more useful example of an insert query can look as the following:

[,typeql]
----
insert
$p isa person,
    has name "Bob",
    has age 31,
    has certified-fortune-teller false;
----

That creates an entity and assigns an ownership of three attributes of different types to the newly created entity.

If any of attributes with the given value didn't exist prior to this query -- TypeDB will create an instance of an
attribute with the required value automatically.

Now to read that data, we can use the xref:typeql::data/get.adoc[Get] query:

[,typeql]
----
match $p isa person;
get;
----

This should return to us all instances of the `person` type. But yet again -- what is the use for entities without their
properties? Let's query for all attributes owned by the `person` type instances:

[,typeql]
----
match
$p isa person,
    has $a;
----

The response should consist of pairs of the `person` type instance with an instance of an attribute type owned by it,
including our `Bob` with the `age` of `31`.

[NOTE]
====
The result of the above query will not return us the first instance of the `person` type we inserted.
It has no attributes. Hence, it doesn't fit in the `match` clause pattern: it doesn't match the `has $a` part.
====

Let's query specifically for `Bob`, age `31`, and retrieve the value of the boolean attribute
`certified-fortune-teller`:

[,typeql]
----
match
    $p isa person, has name `Bob`, has age 31, has certified-fortune-teller $cft;
get $p, $cft;
----

The above query will find every `person` entity that has ownership over the instance of the attribute type `name` with
the value of `Bob`, ownership of the `age` with the value of `31` and the ownership of the `certified-fortune-teller`
attribute with any value.

With the `get` clause, we filter the results to get the `person` instances and the corresponding
`certified-fortune-teller` attribute (represented by the `$cft` variable in the pattern) for every matched result
in a database.

For more information why the `get` clause in the above example needs `$p` see the
xref:typeql:ROOT:data/get.adoc#_number_of_results[Get query] page or the
xref:typeql:ROOT:data/get.adoc#_explanation_of_filter[example explanation].

== Formatting

=== Indentation

Any indentation in TypeQL will be ignored and serves are only for visual aid.

.See an example indentation
[%collapsible]
====
The following three queries are identical in their result:

[,typeql]
----
insert $p isa person, has full-name "Masako Holley", has email "masako.holley@vaticle.com";

insert
$p isa person,
    has full-name "Masako Holley",
    has email "masako.holley@vaticle.com";

insert
$p
isa
person
,
has
full-name
"Masako Holley"
,
has
email
"masako.holley@vaticle.com"
;
----
====

//#todo TypeQL Styleguide

=== Comments

TypeQL uses in-line comments only. To make a comment use the `#` symbol.

.See a comment example
[%collapsible]
====
[,typeql]
----
# This is the first query
insert # this is the insert query without a match clause
$p isa person; # this line inserts a person entity
$p has full-name "Bob"; # we add a constraint to the $p variable used in a previous line

# This is the second query
insert
$p isa person, has full-name "Alex"; # In this query we insert another person
----
====

== Learn more

Learn more essential information about different queries in TypeQL with the xref:typeql::queries.adoc[] page.

Or skip it and go straight to the Schema and Data sections of this documentation for more in-depth look on every query
type.

Use the xref:typeql::grammar.adoc[] page for factual information about syntax of TypeQL.
