= Insert query
:Summary: Insert data to a TypeDB database with an Insert query
:keywords: typeql, typedb, query, insert, write, create
:pageTitle: Insert query

An Insert query adds data to a database.
You can insert an instance of a type, assign ownership of an attribute,
or assign a new role player for a relation.

Insert queries can be used only with write transactions.

== Behavior

An Insert query inserts new data into a TypeDB database using TypeQL patterns.

This is the only type of data query that can be used without a `match` clause.
A `match` clause in an Insert query matches existing concepts in a database to provide context for inserting new data
that is connected to existing data through relations, ownerships, values, types, etc.

An `insert` clause is executed once per every result of preceding `match` clause.
In particular, if there are no match in a database for a `match` clause of an `insert` query,
then there will be no inserts.
If there is no preceding `match` clause, an `insert` clause is executed once.

An insert query returns a Stream/Iterator of ConceptMap with the inserted data.

Insert queries are validated both at a query time and at transaction's commit.
Inserted data must not violate a schema of a database.

== Syntax

A Fetch query consists of an optional
<<_match_clause,match>> clause and an
<<_insert_clause,insert>> clause.

Insert queries are written in TypeQL with the following syntax:

[,typeql]
----
[match <pattern>]
insert <pattern>
----

[#_match_clause]
== Match clause

A `match` clause is optional in an Insert query.
You can use a declarative and composable TypeQL pattern in a `match` clause and TypeDB will find data that matches
the pattern.

For more information on a `match` clause, see the
//#todo update the link after introducing the Match clause page!
xref:typeql::queries.adoc#_match[Match clause] description on the Queries page.

For more information on patterns used in a `match` clause, see the
xref:data/basic-patterns.adoc#_patterns_overview[Pattern syntax] section.

[#_insert_clause]
== Insert clause

An `insert` clause is used in an Insert query to specify new data to be inserted into a TypeDB database.

This clause is executed exactly once for every result matched by the preceding `match` clause of the same query,
or just once, if there is no preceding `match` clause.
An `insert` clause returns inserted data as a stream/iterator of ConceptMap.

Following the `insert` keyword is a TypeQL pattern to define the inserted data.
A pattern in an `insert` clause shares variables with a pattern from a preceding `match` clause

An `insert` clause can have multiple statements in its pattern to insert in one query.
But it can't insert types
(use xref:schema/define-types.adoc[define] to insert new types) and can't have the following:

- Conjunction
- Disjunction
- Negation
- `is` keyword

A single statement of an `insert` clause pattern can insert:

* an instance of a type,
* an ownership of an attribute, or
* a new role player for an existing role of a relationship.

Inserting an ownership of an attribute that doesn't exist in a database yet, implicitly inserts such an attribute.

For more information on patterns used in an `insert` clause, see the examples section below.

For more information on patterns in general, see the
xref:data/basic-patterns.adoc#_patterns_overview[Pattern syntax] section.

== Examples

Use the IAM xref:typedb::attachment$iam-schema.tql[schema] and
xref:typedb::attachment$iam-data.tql[data] for the following insert examples.

=== Inserting data with no matching

To insert a completely new data with no connections to existing data,
we can use an Insert query without a `match` clause:

.Insert with no match
[,typeql]
----
insert $p isa person, has full-name "Bob";
----

The above query inserts a `person` entity that owns a `full-name` attribute whose value is "Bob".

[NOTE]
====
If there is no such attribute in a database, then it will be created by this query implicitly.
====

=== Matching in an Insert query

.Match insert example
[,typeql]
----
match
    $p isa person, has full-name "Bob";
insert
    $p has email "bob@vaticle.com";
----

The above query finds a `person` entity that has the `full-name` attribute with the value of `Bob`
and makes it the owner of the `email` attribute with the value of `bob@vaticle.com`.

If there is no person that matches the `match` clause pattern, then there will be no information inserted by the query.
If there are multiple instances that match the pattern, then every matched person will be assigned ownership of the
email attribute.

[#_insert_entity_example]
=== Inserting entities

Use a variable followed by an `isa` pattern with an entity type to insert an entity.
Entity insertion statement with `isa` is usually combined with statements to assign attribute ownership
or roles in relations.

[NOTE]
====
It is possible to insert an entity without any attribute ownership or roles played.
But entities without any additional information associated with them are indistinguishable from one another.
====

.Inserting entities example
[,typeql]
----
insert
$p isa person,
    has full-name "John Parkson",
    has email "john.parkson@gmail.com",
    has credential "qwerty1";
----

The above query inserts a `person` entity with the following attributes:

* `full-name` -- value is `John Parkson`,
* `email` -- value is `john.parkson@gmail.com`,
* `credential` -- value is `qwerty1`.

=== Inserting attributes

You can insert an attribute explicitly.
Use a variable followed by a value and `isa` pattern with an attribute type to insert an attribute.

.Explicit attribute insertion
[,typeql]
----
insert
$s 34 isa size-kb;
----

You can insert attributes implicitly by assigning ownership of an attribute that doesn't exist yet.
Use the owner's variable (with `isa` pattern if it's a new instance) followed by the `has` keyword,
an attribute type, and a value to insert an ownership of an attribute

.Implicit attribute insertion
[,typeql]
----
insert
$f isa file, has path "new-file.txt";
----

The above query inserts a new `file` entity and assigning it an ownership of `path` attribute
with the value of `new-file.txt`.
If there was no such attribute prior to the query execution, it is inserted implicitly as a result of assigning
the ownership.

We can assign ownership of an attribute for an existing entity or relation by matching it first.
See an example below.

.Implicit attribute insertion for an existing entity
[,typeql]
----
match
$f isa file, has path "new-file.txt";
insert
$f has size-kb 331;
----

The above query matches a `file` entity by its owned `path` attribute and inserts `size-kb` attribute
with the value of `331`.
If there was no such attribute first, it is created as a result of a query.

[NOTE]
====
Note that we removed the `isa file` statement from the insert patter in the above query example.
Since we are matching an existing file in a `match` statement, there is no need to create a new file.
====

// -  #todo Add a link for orphaned attributes and how to delete them

[#_multivalued_attributes]
==== Multivalued attributes

TypeDB supports multivalued attributes by allowing ownership of multiple attributes of the same type.

For example, if the `person` entity type can own the `email` attribute type, then an instance of the `person`
can own multiple (from zero to many) `email` attributes with different values.

.Example of having multiple emails
[,typeql]
----
match
$p isa person, has full-name "John Parkson";
insert
$p has email "john.parkson@vaticle.com",
    has email "admin@jp.com",
    has email "jp@gmail.com";
----

This query will assign ownership of three different attributes of the `email` type to the matched `person` entity
from one of the previous examples: <<_insert_entity_example>>.
As a result, the matched entity should own six attributes, four of which are of `email` type.
See the example of a Fetch query output, showing all attributes of the person with name `John Parkson` below.

.All attributes of the person with four emails
[,typeql]
----
{
    "p": {
        "attribute": [
            { "value": "John Parkson", "value_type": "string", "type": { "label": "full-name", "root": "attribute" } },
            { "value": "admin@jp.com", "value_type": "string", "type": { "label": "email", "root": "attribute" } },
            { "value": "jp@gmail.com", "value_type": "string", "type": { "label": "email", "root": "attribute" } },
            { "value": "john.parkson@gmail.com", "value_type": "string", "type": { "label": "email", "root": "attribute" } },
            { "value": "john.parkson@vaticle.com", "value_type": "string", "type": { "label": "email", "root": "attribute" } },
            { "value": "qwerty1", "value_type": "string", "type": { "label": "credential", "root": "attribute" } }
        ],
        "type": { "label": "person", "root": "entity" }
    }
}
----

See the illustration of the resulted data from xref:clients:ROOT:studio.adoc[TypeDB Studio] below.

image::john-6-attributes.png[John with 4 emails, role=framed, width = 75%, link=self]

=== Inserting relations

Use an optional variable followed by parenthesis with role players references with or without role types.
After the parenthesis use an `isa` pattern with a relation type.

Inserting a relation requires specifying at least one role player.
Roles in a relation are expected to be played by existing or new entities, other relations, or attributes.
A role player can be matched by a preceding `match` clause or inserted by the same `insert` clause.

.Example of inserting a relation
[,typeql]
----
match
$op isa operation, has name "view_file";
insert
$f isa file, has path "new-relation.txt";
$a (object: $f, action: $op) isa access;
----

In the above example we match the `view_file` operation and insert the `new-relation.txt` file to insert a relation
between them with roles `object` and `action` respectively.

We can insert a relation with some roles missing a role player, but that represents an incomplete data state.
The existence of a relation suggests the existence of its role players.

==== Multiple role players

A role in a relation can be played more than once: by multiple role players.

.Relation with multiple role players for a role
[,typeql]
----
match
$p1 isa subject, has full-name "Pearle Goodman";
$p2 isa subject, has full-name "Masako Holley";
$o isa object, has path "zewhb.java";
insert
$obj-ownership (owner: $p1, owner: $p2, object: $o) isa object-ownership;
----

The above query:

. Finds the `person` entity (`$p1`) by its `full-name` attribute `Pearle Goodman`.
. Finds the `person` entity (`$p2`) by its `full-name` attribute `Masako Holley`.
. Finds the `object` entity (`$o`) by its `path` attribute `zewhb.java`.
. Inserts an `object-ownership` relation (`$obj-ownership`) which relates `$p1` (as `owner`) and `$p2` (as
  `owner`) to `$o` (as `object`).

In short, it makes `Pearle Goodman` and `Masako Holley` owners of the `zewhb.java` file.

==== Relations as role players

In addition to entities and attributes, a role of a relation can be played by another relation.

.Example of relation playing a role in another relation
[,typeql]
----
match
$s isa subject, has full-name "Pearle Goodman";
$o isa object, has path "zewhb.java";
$a isa action, has name "modify_file";
$ac (object: $o, action: $a) isa access;
insert
$p (subject: $s, access: $ac) isa permission;
----

The above query:

. Finds the `subject` (`$s`) with the `full-name` attribute with the value of `Pearle Goodman`.
. Finds the `object` (`$o`) with the `path` attribute with the value of `zewhb.java`.
. Finds the `action` (`$a`) whose name attribute has a value of `modify_file`.
. Finds the `access` relation (`$ac`) that relates the `$o` (as `object`) to `$a` (as `action`).
. Inserts a `permission` relation that relates `$s` (as `subject`) to the relation `$ac` (as
`access`).

In short, it creates the `permission` that lets `Pearle Goodman` to modify the `zewhb.java` file.

The relation of `access` type now plays the role of `access` in the inserted relation of the `permission` type.
