= Update query
:Summary: Updating data in a TypeDB database.
:keywords: typeql, typedb, query, update, replace, insert, delete
:pageTitle: Update query

An Update query is used to replace data in a TypeDB database.
This query removes and then adds data based on a given `match`, `delete`, and `insert` patterns.

Update queries can be used only in write transactions.

== Behavior

An Update query deletes concepts and then inserts new concepts to a database.

After matching existing data with a `match` clause,
use a `delete` clause with another TypeQL pattern to specify exact data to delete,
finally, use an `insert` clause with yet another pattern to insert new data instead.

//A `match` clause uses a pattern to match existing data to be changed.
//A `delete` clause uses a pattern to specify data that should be removed.
//A `insert` clause uses a pattern to specify data that should be inserted.

Both `delete` and `insert` clauses are executed once per result of a preceding `match` clause.
In particular, if there is no match in a database for a `match` clause of an Update query,
then there will be no deletions and no insertions.

An Update query returns a lazy Stream/Iterator of ConceptMap with the inserted data.
Inserted data must not violate a schema of a database.

== Syntax

// tag::syntax[]
An Update query consists of a
<<_match_clause,match>>,
<<_delete_clause,delete>>, and
<<_insert_clause,insert>> clauses.

Updates queries are written in TypeQL with the following syntax:

[,typeql]
----
match <pattern>
delete <pattern>
insert <pattern>
----

Both `delete` and `insert` clauses patterns can use variables from a preceding `match` clause pattern and
must have at least one concept variable from a `match` clause of the same query.
// end::syntax[]

[#_match_clause]
== Match clause

A `match` clause is mandatory in an Update query.
You can use a declarative and composable TypeQL pattern in a `match` clause and TypeDB will find data that matches
specified pattern.

For more information on a `match` clause, see the xref:data/match.adoc[] page.

For more information on patterns used in a `match` clause, see the
xref:data/match.adoc#_patterns_overview[Patterns overview] section.

[#_delete_clause]
== Delete clause

include::typeql::data/delete.adoc[tags=delete_clause]

For more information on patterns used to delete data, see the xref:typeql::data/delete.adoc[] page.

[#_insert_clause]
== Insert clause

include::typeql::data/insert.adoc[tags=insert_clause]

For more information on patterns used to insert data, see the xref:typeql::data/insert.adoc[] page.

== Inserting after deleting

There are a few minor unique aspects for the Update query due to the fact of both deleting and inserting
happening in the same query.

By deleting an attribute, we implicitly delete ownership of it from all its owners.
Note that deleting data doesn't affect matched results because `match` clause is executed first.
Even if we delete some data that is used in a preceding `match` clause that will not affect
the set of matched results.
See the <<_delete_attribute,example>> below, where deleting an attribute doesn't block us from replacing it
with another attribute for all its owners.

If we delete a concept used in the subsequent `insert` of the same query, it will produce an error.
For example, deleting an entity and then trying to insert ownership of an attribute to that deleted entity.
You can insert a new concept instead of a deleted one with either the same variable or a new one.

[#_examples]
== Examples

=== Replacing ownership

In many cases, the desired effect is to change the value of an owned attribute.
We can't change the value of an attribute as attributes are immutable, but we can change the owned attribute.
To update an attribute owned by an entity, its ownership must first be removed.
Then, the same owner can be assigned ownership of an attribute with a different value.

.Replacing ownership
[,typeql]
----
match
$p isa person,
    has full-name "Masako Holley",
    has email $email;
delete
$p has $email;
insert
$p has email "m.holley@vaticle.com";
----

The above query does the following:

. Matches all `person` entities (`$p`) that have the `full-name` attribute with the value of `Masako Holley`
amd have some `$email` of type `email`.
. Deletes from `$p` ownership of its current `email` attribute.
. Makes `$p` the owner of the `email` attribute with the value of `m.holley@vaticle.com`.

If there is an existing `email` attribute with the value of `m.holley@vaticle.com`,
the matching `person` entities will now own it.
Otherwise, a new attribute will be created implicitly and owned by the matching `person` entities.

If there are more than one `$email` found for `Masako Holley` person,
than there will be more than one result for the `match` clause.
Both `delete` and `insert` clauses will be executed once for every matched result (for every `$email`).
Inserting multiple ownerships of the same attribute for the same owner produces the same result as inserting one.

[NOTE]
====
The old `email` attribute with the value of `masako.holley@vaticle.com` will still exist,
but it will no longer be owned by the `Masako Holley` person.
====

If you want to narrow down the matched results to a single unique entity,
consider specifying xref:typeql::schema/type-definitions.adoc#_unique_attribute[uniqueness annotation]
for some of its attribute ownerships.

[#_delete_attribute]
=== Replacing attributes

Sometimes you need to delete a matched attribute completely, not only ownership of that attribute.
Deleting an attribute deletes ownership of it from all its owners.
We can insert a new attribute instead of a deleted one without a problem,
because we match all data with a `match` clause pattern before we start deleting and inserting.

.Replacing attribute
[,typeql]
----
match
$p isa person, has full-name $n;
$n contains "inappropriate word";
delete
$n isa full-name;
insert
$p has full-name "deleted";
----

The above query:

. Finds all `person` entities (`$p`) with at least one owned `full-name` attribute (`$n`).
. Filters `$n` (and consequently `$p`) to only those that contain the substring `inappropriate word`.
. Removes these `full-name` attributes that contain the substring from a database.
. Makes `$p` the owner of the `full-name` attribute with the value of `deleted`.

After running the above query, there will be a single `full-name` attribute with a value of `deleted`,
which is owned by the matched entities,
and any `full-name` attributes which contained the substring `inappropriate word` will no longer exist.

=== Updating a relation's role player

To replace a role player, we combine the steps for deleting a role player with extending the relation:

[,typeql]
----
match
$p isa person, has full-name "Pearle Goodman";
$a_write isa action, has name "modify_file";
$a_read isa action, has name "view_file";
$ac_write (object: $o, action: $a_write) isa access;
$ac_read (object: $o, action: $a_read) isa access;
$pe (subject: $p, access: $ac_write) isa permission;
delete
$pe (access: $ac_write);
insert
$pe (access: $ac_read);
----

The above query does the following:

. Finds the `person` entity (`$p`) with the `full-name` of `Pearle Goodman`.
. Finds the `action` entity (`$a_write`) with the `name` of `modify_file`).
. Finds the `action` entity (`$a_read`)  with the `name` of `read_file`).
. Finds all `access` relations (`$ac_write`) that relate any `object` (as `object`) to `$a_write` (as `action`).
. Finds all `access` relations (`$ac_read`) that relate any `object` (as `object`) to `$a_read` (as `action`).
. Finds all permissions (`$pe`) that relate `$p` (as `subject`) to `$ac_write` (as `access`).
. Removes all write accesses (`$ac_write`) as a player of the `access` role in matching permission relations
(`$pe`).
. Adds all read accesses (`$ac_read`) as a player of the `access` role in matching permission relations (`$pe`).

In short, all of Pearle Goodman's permissions with the `modify_file` access
become permissions with the `view_file` access.

After running the above query, all matched `access` relations `$ac_write` with `$a_write` as `action` still
exist but no longer play a role in the matched `permission` relations.
