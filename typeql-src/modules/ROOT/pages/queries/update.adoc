= Update query
:Summary: Updating data in a TypeDB database.
:keywords: typeql, typedb, query, update, replace, insert, delete
:pageTitle: Update query
:page-aliases: typeql::data/update.adoc

An Update query is used to replace data in a TypeDB database.
This query deletes and then inserts data based on a given `match`, `delete`, and `insert` patterns.

For a practical guide on how to send an Update query to TypeDB,
see the xref:manual::writing/update.adoc[] page of TypeDB Manual.

== Syntax

// tag::syntax[]
An Update query consists of a
<<_match_clause,match>>,
<<_delete_clause,delete>>, and
<<_insert_clause,insert>> clauses:

[,typeql]
----
match <pattern>
delete <pattern>
insert <pattern>
----
// end::syntax[]

== Behavior

An Update query deletes concepts and then inserts new concepts, ownerships, or role player assignments to a database.

While a `match` clause pattern is used to match existing data from a database,
the `delete` clause of the same query is used to select the exact data to delete,
finally the `insert` clause is used to insert new data to a database.
Both `delete` and `insert` clauses are executed once per result of a preceding `match` clause.

An Update query returns a lazy Stream/Iterator of ConceptMap with the inserted data.
Inserted data must not violate a schema of a database.

[#_match_clause]
== Match clause

A `match` clause in an Update query matches existing concepts in a database to provide context for deleting and
inserting data specified by the `delete` and `insert` clause pattern respectively.
You can use a declarative and composable TypeQL pattern in a `match` clause and TypeDB will find data that matches
specified pattern.

A `match` clause is mandatory in an Update query.

For more information on patterns and statements used in a `match` clause, see the
xref:typeql::patterns/overview.adoc[Patterns] and xref:typeql::statements/overview.adoc[Statements] sections.

[#_delete_clause]
== Delete clause

include::typeql::queries/delete.adoc[tags=delete_clause]

For more information and examples, see the xref:typeql::queries/delete.adoc[] page.

[#_insert_clause]
== Insert clause

include::typeql::queries/insert.adoc[tags=insert_clause]

For more information and examples, see the xref:typeql::queries/insert.adoc[] page.

== Inserting after deleting

The `insert` clause is executed after the `delete` clause of the same query and uses the same matched result
from the `match` clause.

If we try to delete a concept that is used in the subsequent `insert` clause of the same query,
that creates an error.
For example, deleting an entity and then trying to insert ownership of an attribute to that deleted entity.
You can insert a new concept instead of a deleted one with either the same variable or a new one.

Inserted data doesn't have to be a direct replacement for the data deleted by the same query.

== Replacing ownership

include::typeql::partial$iam-database-links.adoc[]

In many cases, the desired effect is to change the value of an owned attribute.
Attributes are immutable, and we can't change a value of an attribute.
But we can replace ownership: delete ownership of the old attribute and add a new one.

.Replacing ownership
[,typeql]
----
match
$p isa person,
    has full-name "Masako Holley",
    has email $email;
delete
$p has $email;
insert
$p has email "m.holley@typedb.com";
----

The above query does the following:

. Matches all `person` entities (`$p`) that have the `full-name` attribute with the value of `Masako Holley`
amd have some `$email` of type `email`.
. Deletes from `$p` ownership of its matched `email` attribute.
. Makes `$p` the owner of the `email` attribute with the value of `m.holley@typedb.com`.

If there are more than one `$email` found for `Masako Holley` person,
than there will be more than one result for the `match` clause.
Both `delete` and `insert` clauses will be executed once for every matched result (for every `$email`).
Inserting multiple ownerships of the same attribute for the same owner produces the same result as inserting one.

[NOTE]
====
The old `email` attribute with the value of `masako.holley@typedb.com` will still exist,
but it will no longer be owned by the person entity.
====

If you want to narrow down the matched results to a single unique entity,
consider specifying xref:typeql::statements/unique.adoc[uniqueness annotation]
for some of its attribute ownerships.

[#_delete_attribute]
== Replacing attributes

include::typeql::partial$iam-database-links.adoc[]

Deleting an attribute deletes ownership of it from all its owners.
We can insert a new attribute instead of a deleted one without a problem,
because we match all data with a `match` clause pattern before we start deleting and inserting.

.Replacing attribute
[,typeql]
----
match
$p isa person, has full-name $n;
$n contains "inappropriate word";
delete
$n isa full-name;
insert
$p has full-name "deleted";
----

The above query:

. Finds all `person` entities (`$p`) with at least one owned `full-name` attribute (`$n`).
. Filters `$n` (and consequently `$p`) to only those that contain the substring `inappropriate word`.
. Removes these `full-name` attributes that contain the substring from a database.
. Makes `$p` the owner of the `full-name` attribute with the value of `deleted`.

Assuming there are some matches,
after running the above query, there will be a single `full-name` attribute with a value of `deleted`,
which is owned by the matched entities.
Any `full-name` attributes which contained the substring `inappropriate word` will no longer exist in the database.

== Updating a relation's role player

include::typeql::partial$iam-database-links.adoc[]

To replace a role player, we combine the steps for deleting a role player with extending the relation:

[,typeql]
----
match
$p isa person, has full-name "Pearle Goodman";
$a_write isa action, has name "modify_file";
$a_read isa action, has name "view_file";
$ac_write (object: $o, action: $a_write) isa access;
$ac_read (object: $o, action: $a_read) isa access;
$pe (subject: $p, access: $ac_write) isa permission;
delete
$pe (access: $ac_write);
insert
$pe (access: $ac_read);
----

The above query does the following:

. Finds the `person` entity (`$p`) with the `full-name` of `Pearle Goodman`.
. Finds the `action` entity (`$a_write`) with the `name` of `modify_file`).
. Finds the `action` entity (`$a_read`)  with the `name` of `read_file`).
. Finds all `access` relations (`$ac_write`) that relate any `object` (as `object`) to `$a_write` (as `action`).
. Finds all `access` relations (`$ac_read`) that relate any `object` (as `object`) to `$a_read` (as `action`).
. Finds all permissions (`$pe`) that relate `$p` (as `subject`) to `$ac_write` (as `access`).
. Removes all write accesses (`$ac_write`) as a player of the `access` role in matching permission relations
(`$pe`).
. Adds all read accesses (`$ac_read`) as a player of the `access` role in matching permission relations (`$pe`).

In short, all of Pearle Goodman's permissions with the `modify_file` access
become permissions with the `view_file` access.

After running the above query, all matched `access` relations `$ac_write` with `$a_write` as `action` still
exist but no longer play a role in the matched `permission` relations.

== Learn more

[cols-2]
--
.xref:learn::4-writing-data/4.4-updating-data.adoc[]
[.clickable]
****
Learn more about updating/replacing data in a database in the TypeDB Learning course.
****

.xref:manual::writing/update.adoc[]
[.clickable]
****
See how to send an Update query to TypeDB.
****
--

