= Delete query
:Summary: Deleting data from a TypeDB database.
:keywords: typeql, typedb, query, delete, remove
:pageTitle: Delete query
:page-aliases: typeql::data/delete.adoc

A Delete query deletes data from a database.
You can delete a data instance, ownership of an attribute, or a role player reference of a relation.

For a practical guide on how to send a Delete query to TypeDB,
see the xref:manual::writing/delete.adoc[] page of TypeDB Manual.

== Syntax

// tag::syntax[]
A Delete query consists of a
<<_match_clause,match>> and a
<<_delete_clause,delete>> clause.

Delete queries are written in TypeQL with the following syntax:

[,typeql]
----
match <pattern>
delete <pattern>
----

A `delete` clause pattern uses variables from a preceding `match` clause pattern.
// end::syntax[]

[#_behavior]
== Behavior

A Delete query deletes specified attributes, entities, relations, ownerships, and roleplayer references.
//#todo Check whether we call ownerships, etc. concepts

While a `match` clause pattern is used to match existing data from a database,
the `delete` clause pattern of the same query is used to specify the exact data to delete.

A `delete` clause is executed once per every result of a preceding `match` clause.
In particular, if there is no match in a database for a `match` clause of a Delete query,
then there will be no deletions.

A Delete query returns a Promise of an empty response.

[#_match_clause]
== Match clause

A `match` clause is mandatory in a Delete query.
You can use a declarative and composable TypeQL pattern in a `match` clause and TypeDB will find data that matches
specified pattern.

//For more information on a `match` clause, see the xref:data/match.adoc[] page.

For more information on patterns and statements used in a `match` clause, see the
xref:typeql::patterns/overview.adoc[Patterns] and xref:typeql::statements/overview.adoc[Statements] sections.

[#_delete_clause]
== Delete clause

// tag::delete_clause[]
A `delete` clause is used in a query to specify exact data to remove from a database.
Deleted data should be matched first in a preceding `match` clause.

A `delete` clause is executed exactly once for every result matched by a preceding `match` clause of the same query.
// end::delete_clause[]

[WARNING]
====
Since version `2.22.0` of TypeDB a Delete query doesn't fail (throw an error)
if its `delete` clause tries to delete previously deleted or non-existent data.
====

include::typeql::partial$iam-database-links.adoc[]

== Deleting data instances

To delete an entity, attribute, or relation, you need to match them with a `match` clause and then
use the following pattern in a `delete` clause of the same query:

.Deleting an instance of a type syntax
[,typeql]
----
<variable> isa <type>;
----

Where `<variable>` (e.g., `$x`) -- a query variable of a matched instance to delete;
`<type>` (e.g., `person`) -- type or supertype of the deleted instance.

With this pattern, the instance of data is removed completely,
including all roles played and attributes owned by the deleted instance.

Deleting an attribute will result in all owners of such attribute to lose ownership of it.
Deleting a relation will result in all instances that played roles in the relation
to no longer play that role in the deleted relation.

== Deleting ownerships

To delete an ownership of an attribute, match both the attribute and its owner and then use the following pattern
in a `delete` clause of the same query:

.Deleting ownership syntax
[,typeql]
----
<owner-variable> has <attribute-variable>;
----

With this pattern, an attribute itself is not removed,
but rather an ownership of that attribute by that owner is removed.

Attributes are immutable.
Rather than changing the value of an owned attribute,
you can delete an ownership of it and replace with an ownership of another attribute with different value.

For example, to delete ownership of an attribute by any instance,
match both the owner and the attribute and then use a `delete`
clause with a `has` pattern:

.Deleting ownership
[,typeql]
----
match
$p has full-name $fn;
$fn == "Bob";
delete
$p has $fn;
----

The above query finds all `person` entities (`$p`) that have a `full-name` attribute (`$fn`)
whose value is equal to `Bob`.
For every matched pair of `$p` and `$fn` it deletes an ownership of `$fn` by `$p`.

[NOTE]
====
We are not using `isa person` statement in the `delete` clause above because we don't want `$p` to be deleted,
only its ownership of `$fn`.
====

[#_orphaned_attributes]
=== Orphaned attributes
If an attribute is not owned by any other instance, then such an attribute is called orphaned.
Orphaned attributes are not deleted automatically, but quite often, they are
You can easily delete all of them from a database with the following query.

.Deleting orphaned attributes
[,typeql]
----
match
$a isa attribute;
not {$x has $a;};
delete
$a isa attribute;
----

== Deleting a relation

To delete a relation, match it in a database and then use a `delete` clause with an `isa` pattern:

.Deleting a relation
[,typeql]
----
match
$p isa subject, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe isa permission;
----

The above query does the following:

. Finds the `subject` entity (`$p`), with `full-name` attribute value of `Pearle Goodman`.
. Finds all `permission` relations (`$pe`) relating the `subject` entity `$p` (as the `subject` role).
. Deletes all matched permissions `$pe`.

In short, it removes all permissions for `Pearle Goodman`.

[NOTE]
====
We didn't specify all roles of a `permission` relation, e.g., the `object` role.
====

== Deleting role players from a relation

To delete a role player from a relation, match both the relation and the instance of data that plays a role
and then use the following pattern in a `delete` clause of the same query:

.Deleting a role player from a relation syntax
[,typeql]
----
<relation-variable> (<role>:<instance-variable>);
----

Where `<relation-variable>` -- a query variable of the relation to modify;
`<role>` -- an optional specification of a role of a role player to remove;
`<instance-variable>` -- a query variable of a role player instance.

With this pattern, an instance that plays a role in a relation is not removed,
but rather the information of that role player is removed from a relation.

We can omit the role in a pattern if its unambiguous, which role the referenced instance can play in the relation.

[NOTE]
====
Existence of a relation often implies the existence of all of its role players.
Deleting a role player reference from a relation can create an incomplete data state.
Like a `marriage` relation with only one known spouse.
====

[WARNING]
====
Since relations must have at least one role player to exist,
all relations without a role player are implicitly deleted at commit.
====


For example, to delete a role player reference from a relation,
match it in a database and then use a `delete` clause without an `isa` pattern,
specifying just the relation variable and the role player reference in parentheses:

.Deleting a role player reference
[,typeql]
----
match
$p isa subject, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe ($p);
----

The above query does the following:

. Finds the `subject` entity (`$p`), with `full-name` attribute value of `Pearle Goodman`.
. Finds all `permission` relations (`$pe`) relating the `subject` entity `$p` (as the `subject` role).
. From every matched relation `$pe` deletes the reference to the `$p`.

In short, it removes `Pearle Goodman` as the `subject` from all of its `permission` relations.
However, the relations are not deleted, and any other role player references are left intact.

[NOTE]
====
We are not using `isa permission` statement in the `delete` clause above
because we don't want any `$pe` relations to be deleted, only modified by deleting a role player reference from them.
====

[#_simple_example]
== Deleting an instance of data

Instances of data (entities, attributes, and relations) can be deleted by matching them first,
then using a `delete` clause with the `isa` pattern.

.Deleting example
[,typeql]
----
match
$p isa person, has email "john.parkson@gmail.com";
delete
$p isa person;
----

In the above query, `match` clause finds a `person` entity that has the `email` attribute with the value of
`john.parkson@gmail.com`.
Then, it removes the matched entity from a database.

=== Deleting with the use of a supertype

One can delete an instance by using not only its exact type in the `delete` clause pattern,
but also any of its supertypes.
It can be especially useful with matching using inheritance polymorphism.

.Deleting an attribute by its supertype
[,typeql]
----
match
$fn == "Bob";
delete
$fn isa attribute;
----

In the above example we are matching all concepts of any type with the value of `Bob`.
Since only attributes can have a value, all matched concepts are instances of attribute types.
So we can delete all of them be using their common supertype, in this case -- root type `attribute`.
