= Delete query
:Summary: Deleting data from a TypeDB database.
:keywords: typeql, typedb, query, delete, remove
:pageTitle: Delete query
:page-aliases: typeql::data/delete.adoc

A Delete query is used to delete data from a database.
You can delete a data instance, ownership of an attribute, or a role player reference of a relation.

For a practical guide on how to send a Delete query to TypeDB,
see the xref:manual::writing/delete.adoc[] page of TypeDB Manual.

== Syntax

// tag::syntax[]
A Delete query consists of a
<<_match_clause,match>> clause and a
<<_delete_clause,delete>> clause:

[,typeql]
----
match <pattern>
delete <pattern>
----
// end::syntax[]

[#_behavior]
== Behavior

A Delete query deletes specified concepts, ownerships, and role player assignments from a database.

While a `match` clause pattern is used to match existing data from a database,
the `delete` clause pattern of the same query is used to select the exact data to delete.
A `delete` clause is executed once per every result of a preceding `match` clause.

A Delete query returns a Promise of an empty response.

[#_match_clause]
== Match clause

A `match` clause in a Delete query matches existing concepts in a database to provide context for deleting data
specified by the `delete` clause pattern.
You can use a declarative and composable TypeQL pattern in a `match` clause and TypeDB will find data that matches
specified pattern.

A `match` clause is mandatory in a Delete query.

For more information on patterns and statements used in a `match` clause, see the
xref:typeql::patterns/overview.adoc[Patterns] and xref:typeql::statements/overview.adoc[Statements] sections.

[#_delete_clause]
== Delete clause

// tag::delete_clause[]
A `delete` clause is used in a query to specify exact data to be deleted from a database.
Deleted data should be matched first in a preceding `match` clause.
This clause is executed exactly once for every result matched by the preceding `match` clause of the same query.
// end::delete_clause[]

[NOTE]
====
Since version `2.22.0` of TypeDB a Delete query doesn't fail (throw an error)
if its `delete` clause tries to delete previously deleted or non-existent data.
====

To <<_deleting_data_instances,delete a data instance>>, you need to use an
xref:typeql::statements/isa.adoc[isa] statement in an `insert` clause.
To delete ownership of an attribute, use a xref:typeql::statements/has.adoc[has] statement.
Role players can be deleted with a
xref:typeql::statements/role-assignment.adoc[role assignment] statement.
For example, see the <<_deleting_role_players_from_a_relation>> section below.

[#_deleting_data_instances]
== Deleting data instances

include::typeql::partial$iam-database-links.adoc[]

To delete an entity, attribute, or relation,
match it in a `match` clause, then
use the matched concept variable in an `isa` statement with the type or supertype of the data instance.

Deleting an attribute will result in all owners of the attribute to lose ownership of it:

.Deleting an attribute
[,typeql]
----
match
$email "admin@jp.com" isa email;
delete
$email isa email;
----

When a data instance is deleted,
all assignments of roles played and attribute ownerships are also deleted.
The relations, where the roles were played, and attribute, that were owned,
stay in a database, unless deleted explicitly with an `isa` statement.

.Deleting an entity
[,typeql]
----
match
$p isa person, has email "john.parkson@gmail.com";
delete
$p isa person;
----

In the above query, `match` clause finds a `person` entity that has the `email` attribute with the value of
`john.parkson@gmail.com`.
Then, it removes the matched entity from a database.

Deleting a relation will result in all instances that played roles in the relation
to no longer play that role in the deleted relation.

.Deleting a relation
[,typeql]
----
match
$p isa person, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe isa permission;
----

The above query does the following:

. Finds the `subject` entity (`$p`), with `full-name` attribute value of `Pearle Goodman`.
. Finds all `permission` relations (`$pe`) relating the `subject` entity `$p` (as the `subject` role).
. Deletes all matched permissions `$pe`.

In short, it removes all permissions for `Pearle Goodman`.
Notice that we didn't specify all roles of a `permission` relation, e.g., the `object` role.

=== Deleting with the use of a supertype

include::typeql::partial$iam-database-links.adoc[]

A data instance can be deleted by using not only its exact type in a `delete` clause pattern,
but also any of its supertypes.
It can be especially useful in polymorphic queries.

.Deleting an attribute by its supertype
[,typeql]
----
match
$fn == "Bob";
delete
$fn isa attribute;
----

The above query matches and deletes all attributes with the value of `Bob`, regardless of their type.

== Deleting ownerships

include::typeql::partial$iam-database-links.adoc[]

To delete an ownership of an attribute, match both the attribute and its owner and use a
xref:typeql::statements/has.adoc[] pattern in the `delete` clause of the same query:

.Deleting ownership
[,typeql]
----
match
$p has full-name $fn;
$fn == "Bob";
delete
$p has $fn;
----

The above query finds all data instances (`$p`) that have a `full-name` attribute (`$fn`)
whose value is equal to `Bob`.
For every matched pair of `$p` and `$fn` it deletes an ownership of `$fn` by `$p`.

[NOTE]
====
We are not using an xref:typeql::statements/isa.adoc[] statement in the `delete` clause above
because we don't want neither owner nor attribute to be deleted.
Only ownership of one by another.
====

[#_orphaned_attributes]
=== Orphaned attributes

include::typeql::partial$iam-database-links.adoc[]

If an attribute is not owned by any other data instance, then such an attribute is called orphaned.
Orphaned attributes are not deleted automatically, but quite often they are useless.
You can delete all orphaned attributes from a database with the following query:

.Deleting orphaned attributes
[,typeql]
----
match
$a isa attribute;
not {$x has $a;};
delete
$a isa attribute;
----

////
== Deleting a relation

include::typeql::partial$iam-database-links.adoc[]

To delete a relation, match it in a database and then use a `delete` clause with an `isa` pattern:

.Deleting a relation
[,typeql]
----
match
$p isa subject, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe isa permission;
----

The above query does the following:

. Finds the `subject` entity (`$p`), with `full-name` attribute value of `Pearle Goodman`.
. Finds all `permission` relations (`$pe`) relating the `subject` entity `$p` (as the `subject` role).
. Deletes all matched permissions `$pe`.

In short, it removes all permissions for `Pearle Goodman`.

[NOTE]
====
We didn't specify all roles of a `permission` relation, e.g., the `object` role.
====
////

[#_deleting_role_players_from_a_relation]
== Deleting role players from a relation

include::typeql::partial$iam-database-links.adoc[]

To delete a role player from a relation, match both the relation and data instance
that needs to stop playing a role in it, then use xref:typeql::statements/role-assignment.adoc[] statement in the
`delete` clause of the same query.
For example:

.Deleting a role player reference
[,typeql]
----
match
$p isa person, has full-name "Pearle Goodman";
$pe (subject: $p) isa permission;
delete
$pe ($p);
----

The above query does the following:

. Finds the `person` entity (`$p`), with the `full-name` attribute of the `Pearle Goodman` value.
. Finds all `permission` relations (`$pe`) relating the `$p` (as the `subject` role).
. From every matched relation `$pe` deletes the reference to the `$p`.

In short, it removes `Pearle Goodman` as the `subject` role player from all of its `permission` relations.
However, the relations are not deleted, and any other role player references are left intact.

With this query, an instance that plays the role in the relation is not removed,
but rather the information of that role player is removed from the relation.

We can omit the role in a pattern if its unambiguous, which role the data instance can play in the relation.

[NOTE]
====
Existence of a relation often implies the existence of all its role players.
Deleting a role player reference from a relation can create an incomplete data state.
Like a `marriage` relation with only one known spouse.
====

[WARNING]
====
Since relations must have at least one role player to exist,
all relations without a role player are implicitly deleted at committing.
====

[NOTE]
====
We are not using an xref:typeql::statements/isa.adoc[] statement in the `delete` clause
because we don't want any data instances to be deleted.
====

== Learn more

[cols-2]
--
.xref:learn::4-writing-data/4.3-deleting-data.adoc[]
[.clickable]
****
Learn more about deleting data from a database in the TypeDB Learning course.
****

.xref:manual::writing/delete.adoc[]
[.clickable]
****
See how to send a Delete query to TypeDB.
****
--
