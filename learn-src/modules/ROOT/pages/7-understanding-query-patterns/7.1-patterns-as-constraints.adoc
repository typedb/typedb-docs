= Lesson 7.1: Patterns as constraints

== Introduction to patterns

TypeQL uses *pattern matching* as the basis for all data queries. We have seen in previous lessons how almost all queries have a `match` clause. The content of a match clause is a *pattern*, which comprises any number of consecutive statements and specifies what data in the database should be matched. Subsequent clauses in a query then determine the action that should be taken for each match found, for instance retrieving data from each match with a `fetch` clause, inserting data for each match with an `insert` clause, or deleting data from each match with a `delete` clause. Consider the following queries, each of a different kind: Fetch, Insert, Delete, and Update respectively, but with identical `match` clauses.

[,typeql]
----
match
$book has title "The Complete Calvin and Hobbes";
$user has name "Lorenzo Nixon";
$review has score $score;
($book, $review);
$execution ($user, action: $review);
fetch
$execution: timestamp;
----

[,typeql]
----
match
$book has title "The Complete Calvin and Hobbes";
$user has name "Lorenzo Nixon";
$review has score $score;
($book, $review);
$execution ($user, action: $review);
insert
$review has verified true;
----

[,typeql]
----
match
$book has title "The Complete Calvin and Hobbes";
$user has name "Lorenzo Nixon";
$review has score $score;
($book, $review);
$execution ($user, action: $review);
delete
$review isa review;
$rating isa rating;
$execution isa action-execution;
----

[,typeql]
----
match
$book has title "The Complete Calvin and Hobbes";
$user has name "Lorenzo Nixon";
$review has score $score;
($book, $review);
$execution ($user, action: $review);
delete
$review has $score;
insert
$review has score 10;
----

Each of them has the same pattern in the `match` clause.

[,typeql]
----
$book has title "The Complete Calvin and Hobbes";
$user has name "Lorenzo Nixon";
$review has score $score;
($book, $review);
$execution ($user, action: $review);
----

This pattern matches reviews of _The Complete Calvin and Hobbes_ by the user Lorenzo Nixon. Each query then performs a different action for the review(s) matched:

* The Fetch query returns the timestamp of the review's submission.
* The Insert query marks the review as verified.
* The Delete query removes the review.
* The Update query changes the score given by the review.

Understanding how to construct patterns and how they are resolved by TypeDB is necessary for all kinds of data queries, and thus the key to building more expressive queries.

== Constraint satisfaction

Every pattern is equivalent to a list of *constraints* that must be *simultaneously* satisfied. Let's consider the previous pattern. It represents several constraints about the *terms* involved, where a term can be either a variable or a literal:

1. `$book` owns `"The Complete Calvin and Hobbes"`.
2. `"The Complete Calvin and Hobbes"` is of type `title`.
3. `$user` owns `"Lorenzo Nixon"`.
4. `"Lorenzo Nixon"` is of type `name`.
5. `$review` owns `$score`.
6. `$score` is of type `score`.
7. `$book` and `$review` plays roles in the same relation.
8. `$execution` has the role `action`.
9. `$user` plays a role in `$execution`.
10. `$review` plays `action` in `$execution`.

[NOTE]
====
In TypeDB 2.x, the `match` clause of all queries uses all-or-nothing pattern matching, meaning that every constraint must be satisfied simultaneously. Syntax for optional pattern elements will be introduced in TypeDB 3.0, which will allow some constraints to be matched only if possible.
====

The types of `$book`, `$user`, and `$review` are not explicitly specified in the pattern, but we have previously seen how TypeDB is able to infer their types regardless. This is done by solving the pattern as a *constraint satisfaction* problem.

Let's examine the constraints and attempt to determine the type of `$user`. We only know two things about `$user`: constraint (3) tells us that it owns `"Lorenzo Nixon"`, and constraint (9) tells us that it plays a role in `$execution`. This doesn't seem like enough to work with, but we can solve the type of `$user` by combining these constraints with others.

Constraint (4) tells us that `"Lorenzo Nixon"` must be of type `name`. By combining this with constraint (3), we can infer that `$user` must be of a type that owns `name`. Examining the schema, there are five types that do: `user`, `contributor`, `place` (and its subtypes), `company` (and its subtypes), and `promotion`.

Meanwhile, constraint (8) tells us that `$execution` has the role `action`. Examining the schema, there is only one relation type with a role of that name: `action-execution`. Combining this with constraint (9) means that `$user` must be of a type that plays one of its roles, either `action-execution:action` or `action-execution:executor`. Examining the schema again, there are four types that play one of these roles: `user`, `order`, `login`, and `review`.

Finally, we know that `$user` must be one of types `user`, `contributor`, `place`, `company`, or `promotion`, and that it must also be one of types `user`, `order`, `login`, or `review`. As a result, there is only one possible type for `$user`, and that is of course `user`! This fact might seem obvious to us, but this is only because we have named the variables sensibly. If we gave them meaningless names instead, it would be much harder to guess the type of each variable.

[,typeql]
----
$a has title "The Complete Calvin and Hobbes";
$b has name "Lorenzo Nixon";
$c has score $d;
($a, $c);
$e ($b, action: $c);
----

Thus, in order to determine the return types of variables in a query, TypeDB solves each pattern as a constraint satisfaction problem, in which each statement captures one or more constraints. This is the process of *type inference*. In this example, `$user` only had a single possible return type, but we have previously seen queries in which variables have multiple possible return types, notable in xref:learn::3-reading-data/3.2-fetching-polymorphic-data.adoc[Lesson 3.2].

.Exercise
[caption=""]
====
We encountered the following Fetch query in xref:learn::3-reading-data/3.5-query-validation.adoc[Lesson 3.5], which retrieves the IDs of orders being sent to a city other than the city of the user that placed the order. The role names have been omitted now, as they are actually unnecessary for this particular query to be interpreted correctly.

[,typeql]
----
match
($user, $user-city) isa locating;
($order, $user) isa action-execution;
($order, $destination) isa delivery;
($destination, $destination-city) isa locating;
$user-city isa city;
$destination-city isa city;
not { $destination-city is $user-city; };
fetch
$order: id;
----

By identifying constraints present in the query and comparing them to the bookstore schema, determine the return type(s) of `$destination`.

.Schema
[%collapsible]
=====
[,typeql]
----
include::learn::attachment$bookstore-schema.tql[lines=18..]
----
=====

.Answer
[%collapsible]
=====
The only possible return type for `$destination` is `address`, as it is the only type that plays a role in both `delivery` and `locating`.
=====

We encountered the following Fetch query in xref:learn::3-reading-data/3.2-fetching-polymorphic-data.adoc[Lesson 3.2], which retrieves the details of any book that a particular user has interacted with via any kind of system action they performed.

[,typeql]
----
match
$user isa user, has id "u0008";
$book isa book;
(executor: $user, action: $action) isa action-execution;
($book, $action) isa relation;
fetch
$book: isbn, title;
----

As before, determine the return type(s) of `$action`.

.Answer
[%collapsible]
=====
The possible return types for `$action` are `order` and `reivew`, as they are the types that play both `action-execution:action`, as well as a role in a relation in which `book` also plays a role.
=====
====

== Pattern validation

We have seen in
Lessons xref:learn::3-reading-data/3.5-query-validation.adoc[3.5] and xref:learn::4-writing-data/4.5-data-validation.adoc[4.5] that TypeDB validates queries, both to retrieve and to modify data. This is done by resolving the query's patterns via type inference. Previously in this lesson, we have seen how TypeDB infers return types for each variable in a pattern. Now consider the following Fetch query.

[,typeql]
----
match
$x has id $id;
($x, $y, $z) isa publishing;
fetch
$x: attribute;
----

The types that own `id` are `user`, `order`, and `review`, but the types that play roles in `publishing` are `book` (and its subtypes), `publisher`, and `publication`. This means there are no possible types for `$x`, as no type has both necessary capabilities. When solving a pattern's constraints, a solution requires a type for every variable, much like a set of simultaneous equations. If there is no possible return type for any single variable, then there are no solutions for the entire pattern. As a result, this query could not possibly return any data, and so TypeDB returns an error. This validation is performed for patterns in both read queries, as we have just seen, and in write queries, as in the following example.

[,typeql]
----
match
$odyssey isa ebook, has isbn "9780393634563";
insert
$odyssey has stock 20;
----

We encountered this invalid query in xref:learn::4-writing-data/4.5-data-validation.adoc[Lesson 4.5]. The problem here is that `ebook` does not own `stock`. This highlights an additional consideration. If we consider just the pattern in the `match` clause, it is perfectly well-formed. It is only when we combine it with the pattern in the `insert` clause that a problem occurs. The `insert` and `delete` clauses of Insert, Delete, and Update queries also contain patterns, in the same way as `match` clauses. When validating a write query with multiple patterns, TypeDB validates those patterns in *conjunction*, so their constituent constraints must _all_ be solved simultaneously.
