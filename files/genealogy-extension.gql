define

  person sub entity,
    plays wife,
    plays husband,
    plays granddaughter,
    plays grandson,
    plays father-in-law,
    plays mother-in-law,
    plays daughter-in-law,
    plays son-in-law,
    plays son,
    plays daughter,
    plays cousin,
    plays grandparent,
    plays grandchild,
    plays child-in-law,
    plays parent-in-law,
    plays mother,
    plays father,
    plays grandmother,
    plays grandfather;

  marriage sub relationship,
    relates husband,
    relates wife;

  parentship sub relationship,
    relates mother,
    relates father,
    relates son,
    relates daughter;

  mother sub parent;
  father sub parent;
  son sub child;
  daughter sub child;

  siblings sub relationship,
    relates sibling;

  grandparentship sub relationship,
    relates grandparent,
    relates grandfather,
    relates grandmother,
    relates grandchild,
    relates granddaughter,
    relates grandson;

  grandfather sub grandparent;
  grandmother sub grandparent;
  grandson sub grandchild;
  granddaughter sub grandchild;

  cousins sub relationship,
      relates cousin;

  in-laws sub relationship,
    relates child-in-law,
    relates son-in-law,
    relates daughter-in-law,
    relates parent-in-law,
    relates mother-in-law,
    relates father-in-law;

  son-in-law sub child-in-law;
  daughter-in-law sub child-in-law;

  mother-in-law sub parent-in-law;
  father-in-law sub parent-in-law;


  genderizeMarriages sub rule
    when {
      (spouse: $s1, spouse: $s2) isa marriage;
      $s1 has gender "male";
      $s2 has gender "female";
    }, then {
      (husband: $s1, wife: $s2) isa marriage;
    };

  # genderizeMarriages2 sub rule
  #     when
  #     {(spouse: $s1, spouse: $s2) isa marriage;
  #     $s1 has gender "female";
  #     }
  #     then
  #     {(husband: $s2, wife: $s1) isa marriage;};


  male-parent-and-male-child-implies-father-son-parentship sub rule,
    when {
      (parent: $p, child: $c) isa parentship;
      $p has gender "male";
      $c has gender "male";
    }, then {
      (father: $p, son: $c) isa parentship;
    };

  male-parent-and-female-child-implies-father-daughter-parentship sub rule,
    when {
      (parent: $p, child: $c) isa parentship;
      $p has gender "male";
      $c has gender "female";
    }, then {
      (father: $p, daughter: $c) isa parentship;
    };

  female-parent-and-male-child-implies-mother-son-parentship sub rule,
    when {
      (parent: $p, child: $c) isa parentship;
      $p has gender "female";
      $c has gender "male";
    }, then {
      (mother: $p, son: $c) isa parentship;
    };

  female-parent-and-female-child-implies-mother-daughter-parentship sub rule,
    when {
      (parent: $p, child: $c) isa parentship;
      $p has gender "female";
      $c has gender "female";
    }, then {
      (mother: $p, daughter: $c) isa parentship;
    };


  son-of-a-child-of-a-father-implies-grandfather-grandson-grandparentship sub rule,
    when {
      ($p, son: $gc) isa parentship;
      (father: $gp, $p) isa parentship;
    }, then {
      (grandfather: $gp, grandson: $gc) isa grandparentship;
    };

  daughter-of-a-child-of-a-father-implies-grandfather-granddaughter-grandparentship sub rule,
    when {
      ($p, daughter: $gc) isa parentship;
      (father: $gp, $p) isa parentship;
    }, then {
      (grandfather: $gp, granddaughter: $gc) isa grandparentship;
    };

  son-of-a-child-of-a-mother-implies-grandmother-grandson-grandparentship sub rule,
    when {
      ($p, son: $gc) isa parentship;
      (mother: $gp, $p) isa parentship;
    }, then {
      (grandmother: $gp, grandson: $gc) isa grandparentship;
    };

  daughter-of-a-child-of-a-mother-implies-grandmother-granddaughter-grandparentship sub rule,
    when {
      ($p, daughter: $gc) isa parentship;
      (mother: $gp, $p) isa parentship;
    }, then {
      (grandmother: $gp, granddaughter: $gc) isa grandparentship;
    };

  people-with-same-parents-implies-siblings sub rule,
    when {
      (mother: $m, $c1) isa parentship;
      (mother: $m, $c2) isa parentship;
      (father: $f, $c1) isa parentship;
      (father: $f, $c2) isa parentship;
      $c1 != $c2;
    }, then {
      (sibling: $c1, sibling: $c2) isa siblings;
    };

  people-with-sibling-parents-are-cousins sub rule,
    when {
      (parent: $p1, child: $c1) isa parentship;
      (parent: $p2, child: $c2) isa parentship;
      ($p1, $p2) isa siblings;
    }, then {
      (cousin: $c1, cousin: $c2) isa cousins;
    };

  mother-of-a-wife-implies-mother-in-law-to-the-husband sub rule,
    when {
      (husband: $h, wife: $w) isa marriage;
      (mother: $m, $w) isa parentship;
    }, then {
      (mother-in-law: $m, son-in-law: $h) isa in-laws;
    };

  father-of-a-wife-implies-father-in-law-to-the-husband sub rule
    when {
      (husband: $h, wife: $w) isa marriage;
      (father: $f, $w) isa parentship;
    }, then {
      (father-in-law: $f, son-in-law: $h) isa in-laws;
    };

  mother-of-a-husband-implies-mother-in-law-to-the-wife sub rule
    when {
      (husband: $h, wife: $w) isa marriage;
      (mother: $m, child: $h) isa parentship;
    }, then {
      (mother-in-law: $m, daughter-in-law: $w) isa in-laws;
    };

  father-of-a-husband-implies-father-in-law-to-the-wife sub rule
    when {
      (husband: $h, wife: $w) isa marriage;
      (father: $f, child: $h) isa parentship;
    }, then {
      (father-in-law: $f, daughter-in-law: $w) isa in-laws;
    };