= Crash course
:keywords: course, queries, typedb, typeql, tutorial, quickstart, insert, fetch
:page-aliases: home::25-queries.adoc
:pageTitle: Crash course
:summary: Learn about schema definitions and data operations in TypeDB.

This crash course gives a quick overview of the main ingredients for building your application with TypeDB. For an in-depth dive into these topics, check out the xref:academy::overview.adoc[TypeDB Learning Course].

== Key ingredients

TypeDB is a new kind of database that combines ideas from classical databases and modern high-level programming languages. To get started with TypeDB, let's have a look at three key components and ideas underlying TypeDB's design.

*1. <<_type_system,The type system>>*. TypeDB is built on a rich type system enabling us to directly capture the structure and flow of the data we work with.

.A simple schema with six inter-dependent types
[,typeql]
----
define
  file sub entity, owns name, owns creation-date;
  image sub file, plays project-inclusion:asset;
  video sub file, plays project-inclusion:asset;
  project sub entity, owns name, plays project-inclusion:project;
  project-inclusion sub relation, relates project, relates asset;
  creation-date sub attribute, value datetime;
  name sub attribute, value string;
----
TypeDB's conceptual data model can express object-oriented constructs like abstraction, inheritance, and polymorphism, which means we can easily represent various data structures including relational-, graph-, and document-like data.

*2. <<_database_essentials,The querying paradigm>>*. TypeDB approaches database management through a simple, pattern-based querying paradigm.

.Find images and videos that are part of the same project and have the same creation date, and return their names
[,typeql]
----
match
  $thumbnail isa image, has creation-date $date;
  $video isa video, has creation-date $date;
  (project: $project, asset: $thumbnail) isa project-inclusion;
  (project: $project, asset: $video) isa project-inclusion;
fetch
  $thumbnail: name;
  $video: name;
----
Building on TypeDB's conceptual data model, this provides us with a highly versatile toolset that resolves a range of deep engineering challenges.

*3. <<_patterns,The power of patterns>>*. Patterns are used in TypeDB to interact with data and types of all sorts.

.Add files older than 2010 to the "archive" project
[,typeql]
----
match
  $archive isa project, has name "archive";
  $old-file isa image, has creation-date < 2010-01-01;
insert
  (project: $archive, asset: $old-file) isa project-inclusion;
----
Patterns often read just like statements in natural language, and can be easily composed to form new patterns. This makes many tasks that are arduous in traditional databases simple and elegant in TypeDB.

In the next sections, we dive deeper into each of the above topics. Note that this crash course covers only the very basics of TypeDB! For a more comprehensive overview, check out our xref:academy::overview.adoc[Learning Course].
// we can infer new data on-the-fly using rules, which allow us to embed the logic of our data directly into our schema.

[NOTE]
====
To follow the examples on this page, make sure to create a TypeDB database with sample xref:home::quickstart.adoc#_define_a_schema[schema] and xref:home::quickstart.adoc#_load_data[data] for a permission system, as instructed in the xref:quickstart.adoc[Quickstart Guide].
====

[#_type_system]
== The type system

TypeDB is based on a novel conceptual data and querying model, the xref:academy::9-modeling-schemas/9.1-the-pera-model.adoc[PERA model]. This modern database model was introduced in recent https://typedb.com/papers/typeql-theory[research], and aims to provide higher levels of flexibility and expressivity when working with structured data.

=== Schema and types

A TypeDB database has a *schema* that defines the *types* that comprise a data model. A new type can be added by *subtyping* an existing type: either a previously defined type of one of the three built-in *root types*: `entity`, `relation`, and `attribute`.

A schema comprises three distinct hierarchies of entity types, relation types, and attribute types descended from their respective root types. The properties of these types are summarized in the following table.

[#_root_types]

.Root types and their properties
[cols="^.^h,^.^,^.^,^.^",options="header"]
|===
| | Entity types | Relation types | Attribute types
| Contain | objects | objects | values
| Dependencies | none | roleplayers | owners
| Interfaces | none | roles | ownership
| Can implement interfaces | ✓ | ✓ | ✗
| Can be subtyped | ✓ | ✓ | if abstract
| Can be abstract | ✓ | ✓ | ✓
|===

You can learn more about the PERA model in xref:academy::9-modeling-schemas/9-modeling-schemas.adoc[Lesson 9] of the Learning Course.

=== Data instances and values

Data is inserted into a TypeDB database by instantiating of types in the schema.

* *Entities* (instances of entity types) represent the independent objects in a data domain.

* *Relations* (instances of relation types) represent linkages between objects. The ways in which relations can link data are defined in the schema by a relation type's *roles*, which other types can play.

* *Attributes* (instances of attribute types) represent properties of other data instances, and always have a *value* associated to them (e.g. an integer, a string, or a datetime). The attributes that a type can own are specified in the schema by an attribute type's *ownerships*.

[#_database_essentials]
== The querying paradigm

All TypeQL queries use fully declarative and composable patterns to express constraints on the database, without considering implementation details. This likewise applies to reading or writing data instances and type definitions, and follows the spirit of _"(infra)structure as code"_: you declare it, TypeDB does it!

Unlike other databases, there is no need to explicitly specify "data transformations" (like joins or other table/document transformations) and no need for thinking too hard about query optimizations, as the query engine will take care of query planning and execution.

=== Query types

TypeDB comes with a handful of basic query types.

* *Define* and *Undefine* queries are used to create and modify the schema.
* *Insert*, *Delete*, and *Update* queries are used to insert and modify data.
* *Fetch* and *Get* queries are used to read data.

[#_fetch_queries]
=== Fetch queries

A xref:typeql::queries/fetch.adoc[Fetch query] projects types and values of data instances matched by selected variables into a JSON-format response. The following query matches a user by username and fetches their name.

[,typeql]
----
include::home::partial$cc-queries/subquery.tql[lines=1..4]
----

This query makes use of xref:typeql::queries/fetch.adoc#_customize_output[customized output labels] for the JSON fields.

A Fetch query can also be incorporate a xref:typeql::queries/fetch.adoc#_subqueries[subquery]. In the following query, we add a subquery to fetch the paths of all files that the user has a permission on.

[,typeql]
----
include::home::partial$cc-queries/subquery.tql[]
----

[#_get_queries]
=== Get queries

A xref:typeql::queries/get.adoc[Get query] returns stateful objects representing data instances matched by selected variables. After retrieving them, they can be programmatically manipulated with driver API methods. The following Get query retrieves all file entities as stateful objects.

[,typeql]
----
include::home::partial$cc-queries/get-count-files.tql[lines=1..3]
----

The objects can also be operated on directly within the query using aggregators, like the `mean` operator.

[,typeql]
----
match
  $f isa file, has size-kb $s;
get $f, $s;
mean $s;
----

[#_insert_queries]
=== Insert queries

An xref:typeql::queries/insert.adoc[Insert query] inserts data into a database. They can be constructed with or without a `match` clause, which allows newly inserted data to reference existing data in the database.

The following Insert query uses an `insert` clause without a `match` clause to insert data that is not related to existing data.

[,typeql]
----
include::home::partial$cc-queries/insert.tql[]
----

This query inserts a new `user` entity, with `username`, `name`, and `email` attributes. The inserted user has no connection to existing data in a database.

To insert new data that is connected to existing data, we use a `match` clause, as in the following query. It matches an existing user and inserts a new email for this user.

[,typeql]
----
include::home::partial$cc-queries/match-insert.tql[]
----

An `insert` clause is executed once for every tuple of variables found by the `match` clause, and inserts data using the context of these variables.

[#_delete_queries]
=== Delete queries

A xref:typeql::queries/delete.adoc[Delete query] deletes data from a database. In a Delete query, we first identify the data to be deleted using a `match` clause, and then specify what data to delete. In the following query, we match a specific user by their username, and delete one of their emails.

[,typeql]
----
include::home::partial$cc-queries/delete.tql[]
----

In the `delete` clause, we specify exactly which constraints should be deleted: the ownership of the email by the user. This particular query does not delete the user or the attribute itself. Indeed, the email attribute can be potentially owned by some other data instances. In contrast, deleting the attribute will automatically delete all ownerships of it.

As with Insert queries, the `delete` clause of a Delete query is executed once for each tuple of variables matched by the preceding `match` clause. If a `match` clause matches no data, then nothing will be deleted.

[#_updates]
=== Update queries

Update queries combine `match`, `delete`, and `insert` clauses to replace or otherwise update data stored in a database. In the following example, we match a user with the username `bob_93` and replace their email with a new one.

[,typeql]
----
include::home::partial$cc-queries/update.tql[]
----

Note that if the user doesn't have any emails, then there will be no deletes or inserts. Similarly, if the user has multiple emails, each of them will be matched and replaced. Since there can be only one attribute with a given type and value, and attributes can be owned only once by each object, the inserts will be idempotent in this particular case: the query will delete _all_ the user's emails and then insert _one_ new one.

[#_extend_schema]
=== Schema extensions made easy

We can extend our schema by adding new types, or by adding new capabilities to existing types. Due to the typed, pattern-based database operations, many existing queries do not need to be manually adjusted to reflect the changes, but instead "adapt automatically", as we will see later on.

First, we can modify or extend the schema of a database with a Define query. In the following example, we extend our sample schema by giving the `user` entity type ownership of a new attribute type `rating`.

[,typeql]
----
define
  rating sub attribute, value double;
  user owns rating;
----

Now, we add some rating data: let's add one attribute for the existing user `bob_93` and one for a new user.

[,typeql]
----
match
  $u1 isa user, has username "bob_93";
insert
  $u1 has rating 0.99;
  $u2 isa user,
    has username "not_bob",
    has rating 0;
----

If we extend one of the <<_fetch_queries, previous Fetch queries>>, we can easily retrieve the new attribute.

[,typeql]
----
match
  $u isa user, has username "bob_93";
fetch
  $u: name, rating;
----

Of course, this example is quite simple, but the "modular" approach of type dependencies, via relation roles and attribute ownership, often makes schema modification extremely convenient. Moreover, this can be combined with built-in support for <<_inheritance_polymorphism,inheritance polymorphism>> or the usage of xref:academy::3-reading-data/3.4-fetching-schema-types.adoc[type variablization] (both enabled by TypeDB's type inference) to organically incorporate schema changes into existing queries.

[#_patterns]
== The power of patterns

TypeQL uses declarative xref:typeql::patterns/overview.adoc[patterns] to seek and manipulate data in a database. Every statement is a constraint to be satisfied by the query engine.

Variables indicate unknowns that can be matched by a data instance in the database. Solving a pattern is like solving a system of simultaneous equations, where every equation (statement) must be satisfied, and a solution (result) is a set of values for variables that make every equation in the system a true statement.

=== Matching patterns

The following query uses a pattern to match all data instances `$u` of the `user` type in a database and, for every matched instance `$u`, retrieves all its owned names and emails.

[,typeql]
----
include::home::partial$cc-queries/list-users.tql[]
----

The above is a xref:typeql::queries/fetch.adoc[Fetch query], with a `fetch` clause that projects every matched variable `$u` into a stream of JSON-format responses, as follows.

//#todo Automate testing
[,json]
----
{
    "u": {
        "email": [  ],
        "name": [ { "value": "Alex", "type": { "label": "name", "root": "attribute", "value_type": "string" } } ],
        "type": { "label": "user", "root": "entity" }
    }
}
{
    "u": {
        "email": [ { "value": "bob@typedb.com", "type": { "label": "email", "root": "attribute", "value_type": "string" } } ],
        "name": [ { "value": "Bob", "type": { "label": "name", "root": "attribute", "value_type": "string" } } ],
        "type": { "label": "user", "root": "entity" }
    }
}
----

=== Composing patterns

TypeQL patterns consist of TypeQL xref:typeql::statements/overview.adoc[statements], which can be combined in any order in an implicit xref:typeql::patterns/conjunction.adoc[conjunction], and using other logical pattern operations like xref:typeql::patterns/disjunction.adoc[disjunctions] or xref:typeql::patterns/negation.adoc[negations]. Let's modify the previous query's pattern by adding three new constraints.

[,typeql]
----
include::home::partial$cc-queries/fetch-all-bob-emails.tql[]
----

This pattern now describes four constraints that must be satisfied:

. The data instance `$u` is of type `user`.
. The data instance `$n` is of type `name`.
. The data instance `$u` must own the attribute `$n`.
. The attribute `$n` has the string value `"Bob"`.

We can also express these constraints more concisely using a single *composite statement*, as follows.

[,typeql]
----
match
  $u isa user, has name "Bob";
fetch
  $u: name, email;
----

The variable `$n` is now implicit, but the pattern is still broken down into the same set of constraints when the query is resolved. This is an example of a composition of patterns with a logical conjunction, but simply reading the query out loud should make it clear what is going on!

The result of the above query now includes only one user that matches the new pattern:

[,json]
----
{
    "u": {
        "email": [ { "value": "bob@typedb.com", "type": { "label": "email", "root": "attribute", "value_type": "string" } } ],
        "name": [ { "value": "Bob", "type": { "label": "name", "root": "attribute", "value_type": "string" } } ],
        "type": { "label": "user", "root": "entity" }
    }
}
----

=== Relation patterns

Relations in TypeDB make expressing data dependencies simple and efficient, without the need for joins, tables, foreign keys, or any other “hacks” to model complex data. A relation type collects “linkages” between objects of other types. 

The specification of these linkages is defined in the schema: a relation type has *roles* which can be *played* by other types. For example, in the xref:home::quickstart.adoc#_define_a_schema[sample schema] for this crash course, we defined the `permission` relation.

[,typeql]
----
permission sub relation,
  relates object,
  relates subject,
  relates action;
user plays permission:subject;
file plays permission:object;
action plays permission:action;
----

Based on this, let's insert a relation of the `permission` type, between a user with username "al-capucino", a file with the path "README.md", and the action with name "write".

[,typeql]
----
match
  $u isa user, has username "al-capucino";
  $f isa file, has path "README.md";
  $a isa action, has name "write";
insert
  (subject: $u, object: $f, action: $a) isa permission;
----

In the above example, we first match the existing user, file, and action, and then insert the `permission` relation, with `$u` playing the `subject` role, `$f` playing the `object` role, and `$a` playing the `action` role.

==== N-ary relations

Relations in TypeDB are simple but flexible. For example, you can create an n-ary relation by defining any number of roles in the relation's type definition. These n-ary relations generalize basic binary relations.

* A binary relation has two roles, for example, `group-membership` with roles `group` and `member` (or alternatively a single role played twice!). Binary relations are a frequently occurring structure in data, and have been popularized in the context of graph databases.
* A ternary relation is a relation with three roles, for example the `permission` relation we have defined above. Unlike binary relations, ternary relations cannot be modelled in graph databases without employing techniques like https://en.wikipedia.org/wiki/Reification_(computer_science)[reification].
* Similarly, an n-ary relation is a relation with n roles for any number n > 0.

Every role has a set of types capable of playing it, defined in the schema. For example, a `user` type could be defined to play the role of `member`, and `user-group` may play the role of `group`.

=== Attribute patterns

Attributes in TypeDB are used to store values. Every attribute type is defined in the schema to be of a specific value type (e.g. integers or strings). The schema also specifies which entity and relation types *own* a given attribute type.

To create an attribute, we instantiate an attribute type using a particular value, that is of the associated value type. An attribute can be created explicitly by using an xref:typeql::statements/isa.adoc[isa statement] with xref:typeql::statements/value-assignment.adoc[value assignment]:

[,typeql]
----
insert
  $name "James" isa name;
----

We refer to such as attribute as *independent*: for now, it is not owned by any object!

We can also create attributes implicitly. When we assign ownership of an attribute, if the attribute doesn't exist in the database, it will be created automatically.

[,typeql]
----
insert
  $p isa person, has name "Sam";
----

==== Owning multiple attributes

By default, multiple attributes of the same type can be owned by the same data instance, sometimes known as *multivalued attributes* in other database systems. In the following example, we insert a new person that owns two names: "Bob" and "Another Bob":

[,typeql]
----
include::home::partial$cc-queries/attributes.tql[lines=6..9]
----

You can limit behavior this by using a xref:typeql::statements/key.adoc[key constraint], which adds a constraint on the number of owned attributes to be equal to exactly one.

==== Globally unique attributes

In TypeDB's data model, any attribute can be uniquely addressed by its type and value. Thus, there can be not be more than one attribute of the same type and value. This optimizes storage space and memory consumption as data is naturally deduplicated.

[,typeql]
----
include::home::partial$cc-queries/attributes.tql[lines=11..14]
----
For example, if multiple people have the same name, they have ownership of the same attribute, as shown in the following diagram, representing the result of the above query.

image::home::bobs.png[Multiple Bobs, role=framed, width = 50%, link=self]

It is useful to think of attribute values as immutable: you can’t change the value of an attribute (since the attribute _is_ its value and type), but you can delete ownership of it and insert ownership of another, for example with an <<_updates,Update query>>.

[#_polymorphism]
== Polymorphic queries

The full power of patterns and type inference can be seen by using polymorphism in queries. This allows us to relax and combine type constraints in novel ways. There are three types of polymorphism in TypeQL queries:

* <<_inheritance_polymorphism,Inheritance polymorphism>>
* <<_interface_polymorphism,Interface polymorphism>>
* <<_parametric_polymorphism,Parametric polymorphism>>

[#_inheritance_polymorphism]
=== Inheritance polymorphism

Inheritance polymorphism allows us to query for a supertype and retrieve results including all of its subtypes. In the following query, we retrieve the names of all people in the database.

[,typeql]
----
include::home::partial$cc-queries/inheritance-polymorphism.tql[]
----

The variable `$p` will match instances of both the `person` type and of the `user` type, because `user` is a subtype of `person`. To retrieve only instances of the exact type being queried, we can use the `isa!` keyword instead. See the xref:typeql::statements/isa.adoc[] page for more information.

[#_interface_polymorphism]
=== Interface polymorphism

Interface polymorphism lets us query for any types that implement a given interface. By implementing a given interface, we mean owning an attribute type or playing a role in a relation type (see the <<_root_types,root types table>>). In the following query, we match all data instances that have a name, regardless of their types:

[,typeql]
----
include::home::partial$cc-queries/interface-polymorphism.tql[]
----

The above example can match instances of any type for the variable `$x`, as long as it's a data instance owns a `name` attribute.

[#_parametric_polymorphism]
=== Parametric polymorphism

Parametric polymorphism lets us retrieve data _independently of the schema_. Parametric queries work by constraining data structure rather than data semantics: the only type labels used are those of the root types. In the following query, we match any data instance that has an attribute of any type and a numeric value larger than `100`. We then retrieve all attributes of those data instances.

// [,typeql]
// ----
// include::home::partial$cc-queries/parametric-polymorphism.tql[]
// ----

[,typeql]
----
match
  $x has $a;
  $a > 100;
fetch
  $x: attribute;
----

The result of the above query, when sent to our sample database from the xref:home::quickstart.adoc[Quickstart Guide], includes a `file` entity owning a `size-kb` attribute, which indeed has a value larger than `100`.

[,json]
----
{
    "data": {
        "attribute": [
            { "value": 3458761, "type": { "label": "size-kb", "root": "attribute", "value_type": "long" } },
            { "value": "docs/quickstart-guide.adoc", "type": { "label": "path", "root": "attribute", "value_type": "string" } }
        ],
        "type": { "label": "file", "root": "entity" }
    }
}
----

[#_rules]
== Rule-based inference

TypeDB can perform rule-based inference in Fetch and Get queries. Rules are defined alongside types in the schema of a database. When retrieving data in a read transaction with inference enabled, TypeDB can add inferred "virtual" data to the results. The inferred data is never persisted and exists only in the scope of the transaction. When the transaction is closed and new one opened, the data is inferred again to ensure that it is always up-to-date.

In the following query, we add a rule to infer read permissions on an object for any subject that already has write permissions or full permissions.

[,typeql]
----
include::home::partial$cc-queries/inference-rule.tql[]
----

We also make use of interface polymorphism directly in this rule: it will work regardless of the types of `$s` and `$o`!

To see this rule in action, xref:manual::reading/infer.adoc#_how_to_send_a_query_with_inference[enable inference] and run the following Fetch query.

[,typeql]
----
include::home::partial$cc-queries/inference-fetch.tql[]
----

The above query matches all read permissions that people have on files, and returns the name of the person and the path of the file. Since we are only matching read permissions, and there is no such permission stored in our database, all results returned must be obtained though inference with the rule we inserted earlier!

=== Combining rules

Each rule can infer a single relation or a single attribute ownership, but a rule can be applied many times, as long as it's producing new results. We can use multiple rules in a schema, and also combine them with advanced technics, like logical xref:academy::10-using-inference/10.2-rule-chaining.adoc[chaining] (including recursion) or xref:academy::10-using-inference/10.3-rule-branching.adoc[branching]!
