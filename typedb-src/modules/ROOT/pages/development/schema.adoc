= Defining schema
:Summary: How to define a TypeDB database schema.
:keywords: typedb, typeql, schema, define, undefine, types, rules
:longTailKeywords: typedb schema, database schema, define a type, define a rule
:pageTitle: Defining schema

[#_schema]
== Schema

A TypeDB schema is like a blueprint of a database.

A schema contains all user-defined xref:fundamentals/types.adoc#_type[types] and xref:fundamentals/inference.adoc#_rules[rules]
that can be used in the database.

The types in a database schema define the data model, its constraints, and the vocabulary used in TypeQL queries to
the database.

== How to define a schema

To define or undefine anything in a TypeDB database schema we need:

1. Build the TypeQL query string (see below on this page):
    * See how to <<_define_types,define types>>;
    * See how to <<_define_rules,define rules>>;
    * See how to <<_undefine_types,undefine types>>;
    * See how to <<_undefine_rules,undefine rules>>;
    * See how to <<_modify_types, modify types>>
2. xref:development/connect.adoc#_clients[Connect] to a TypeDB database with a xref:development/connect.adoc#_sessions[SCHEMA session].
3. Run a proper query type (xref:fundamentals/queries.adoc#_define_query[Define]/ xref:fundamentals/queries.adoc#_define_query[Undefine])
   with a query string we built in a xref:development/connect.adoc#_transactions[WRITE transaction].
4. Commit the transaction to persist the changes.

[#_define_types]
== Define types

Any number of define statements can be combined in a single Define query, requiring only one `define` clause with only
one `define` keyword at the very beginning.

To define a type use the following syntax:

[,typeql]
----
define

<label> sub <parent type label>
    [(, abstract)]
    [(, owns <attribute type label> [@annotation])...]
    [(, plays <relation type label>:<role>)...];
----

The `define` keyword is used only once per query for any number of statements.

The `abstract` keyword makes the type xref:fundamentals/types.adoc#_abstract_types[abstract].

The `owns` keyword defines an attribute ownership.

The `plays` keyword defines a role that this type can play.

.See examples
[%collapsible]
====
[,typeql]
----
define object sub entity;
----

In the above example we define the `object` type as a subtype of the `entity` type, which is a built-in root type.

Here is more complex example:

[,typeql]
----
define

object sub entity;
resource sub object;
file sub resource,
   owns path,
   owns size-kb,
   plays object-ownership:object;
----

If we try to run this example in an empty database it will throw an error, because the `path` and `size-kb` attribute
types we mentioned as owned by the `file` entity type actually need to exist in the schema.

They can be defined in the schema prior to our query (e.g., if we run this query on the database from the
xref:quickstart.adoc[Quickstart] page), or we can define them in the same query. To do that, use the following query
instead:

[,typeql]
----
define

object sub entity;
resource sub object;
file sub resource,
   owns path,
   owns size-kb,
   plays object-ownership:object;

path sub attribute, value string;
size-kb sub attribute, value long;

object-ownership sub relation,
   relates object;
----
====

[NOTE]
====
All types, mentioned in a type definition (owned attributes or relation that the type can play a role in) must exist
in the schema or be defined in the same query.
====

[#_define_entity_type]
=== Define entity type

See the xref:_define_types[] section.

No specific syntax for entities.

For more information about entity types see the xref:fundamentals/types.adoc#_entity_types[Entity types] section in
xref:fundamentals/types.adoc[] page.

[#_define_relation_type]
=== Define relation type

To define a relation type use the following syntax:

[,typeql]
----
define

<label> sub (<relation type label>)
   [(, abstract)]
   [(, owns <attribute type label> [@annotation])...]
   (, relates <role label>) [(, relates <role label>)...]
   [(, plays <relation type label>:<role>)...];
----

The `relates` keyword defines a role of the relation type. There must be at least one role for any relation type.

.See example
[%collapsible]
====
[,typeql]
----
define

ownership sub relation,
    relates owned,
    relates owner;

group-ownership sub ownership,
    owns ownership-type,
    relates group as owned;
----

In the above example we define:

* the `ownership` type as a subtype of the `relation` root type, with:
** `owned` role,
** `owner` role;
* and the `group-ownership` type as a subtype of the `ownership` type, with:
** `ownership-type` role,
** `group` role, overriding inherited `owned` role,
** inherited `owner` role.
====

For more information about relation types see the xref:fundamentals/types.adoc#_relation_types[Relation types] section in
xref:fundamentals/types.adoc[] page.

[#_define_attribute_type]
=== Define attribute type

To define an attribute type use the following syntax:

[,typeql]
----
define

<label> sub (<abstract attribute type label>)
[(, abstract)]
, value <value type> [, regex "<regex-expression>"]
[(, owns <attribute type label> [@annotation])...]
[(, plays <relation type label>:<role>)...];
----

The `value` keyword is mandatory and used to define a value type for the attribute.

For more information about attribute types see the xref:fundamentals/types.adoc#_attribute_types[Attribute types] section in
xref:fundamentals/types.adoc[] page.

[#_define_rules]
== Define rules

To define a rule use the following syntax:

[,typeql]
----
define

rule <rule-label>:
when {
    ## the conditions
} then {
    ## the conclusion
};
----

Rule label must be unique. Defining a rule with existing label will rewrite the old rule with the new one.

The condition definition goes inside the `when` clause.

The conclusion definition goes inside the `then` clause.

For more information about rules see the xref:typeql::schema/define-rules.adoc[,window=_blank] page.

[#_undefine]
== Undefine

Undefine query is used to remove a type or a rule definition form a schema.

Any number of undefine statements can be combined in a single Undefine query, requiring only one `undefine` clause
with only one `undefine` keyword at the very beginning.

[#_undefine_types]
=== Undefine types

To undefine a type use the following syntax:

[,typeql]
----
undefine

<label> sub <parent type label>
    [(, owns <attribute type label> [@annotation])...]
    [(, plays <relation type label>:<role>)...];
----

The undefine `keyword` is used only once per query for any number of statements.

The `sub` keyword is used only to remove the type mentioned left from the keyword from a schema.
The parent type label must be a direct or indirect supertype.

The `owns` keyword undefines an attribute ownership.

The `plays` keyword undefines a role that this type can play.

.See examples
[%collapsible]
====
Let's define a few new types to undefine them later.

[,typeql]
----
define

tag sub attribute, value string;

connection sub relation,
   relates item;

item sub entity,
   owns tag,
   plays connection:item;
----

In the above example we define the `tag` attribute type, `connection` relation type with the `item` role,
and `item` entity type, that can owns `tag` attribute type and plays `connection:item` role.

To undefine an attribute ownership use the following query:

[,typeql]
----
undefine

item owns tag;
----

To undefine the ability to play the role `connection:item` use the following query:

[,typeql]
----
undefine

item plays connection:item;
----

To undefine the `item` entity type (remove it from the schema) use the following query:

[,typeql]
----
undefine

item sub entity;
----
====

[#_undefine_subtype]
[IMPORTANT]
====
To be able to remove a type we need to delete all instances of data and all subtypes of this type first.

The usage of `sub` keyword in the undefine query statement will remove the type on the left from the `sub` completely
from the schema regardless of any ownerships or roles mentioned for this type.
====

[#_undefine_rules]
=== Undefine rules

To undefine a type use the following syntax:

[,typeql]
----
undefine

rule <rule-label>;
----

== Modification

[#_modify_types]
=== Modify types

The define statements are idempotent. By sending the same define query twice or more times the very same resulting
schema must be achieved as if we send it only once. So types and/or rules will not be duplicated.

We can add ownership of an attribute, annotation to an ownership, or a role to play by just defining the add-on.

.See example
[%collapsible]
====
Let's define a new ownership for the `item` entity.

[,typeql]
----
define

item owns size;
----

In the query above we define an ownership of `size` attribute by the `item` type.
For the query to succeed we need both the `item` and the `size` types to exist in the schema already.
====

=== Rename types

include::typeql::schema/modify.adoc[tag=rename]

=== Modify rules

To modify a rule xref:_define_rules[define a new rule] with the same label. It will overwrite the existing rule upon
commit.

== Learn more

After we define the schema of our database we can try xref:typedb::development/write.adoc[writing] and
xref:typedb::development/read.adoc[reading] data from the database.

In case there is no database schema ready yet, we can use the xref:attachment$iam-schema.tql[IAM schema,window=_blank]
to try all the queries.
